/// It extends the `std.mathspecial` module to support `GDN` objects.

module ad.math.special;

public import std.mathspecial;

import std.math: getNaNPayload, isInfinity, isNaN, signbit, trunc;
import std.traits: select;

static import ad.math.polygamma;

import ad.core;
import ad.math.internal:
    areAll, asGDN, asReal, CommonGDN, dirac, getNaNPayload, isGDN, isGDNOrReal, isOne, isNaN,
    signbit, sgn;


private pure nothrow @nogc @safe GDN!Deg polygamma(ulong N, ulong Deg)(in GDN!Deg g) if (N > 0)
{
    static if (Deg == 1)
        const df = ad.math.polygamma.polygamma!(N+1)(g.reduce());
    else
        const df = polygamma!(N+1)(g.reduce());

    return GDN!Deg(ad.math.polygamma.polygamma!N(g.val), df*g.d);
}

unittest
{
    import std.format: format;

    const q = GDN!1(ad.math.polygamma.polygamma!1(1), ad.math.polygamma.polygamma!2(1));
    assert(polygamma!1(GDN!1(1)) is q);

    const a_exp = GDN!1(ad.math.polygamma.polygamma!1(2), 3*ad.math.polygamma.polygamma!2(2));
    const a_act = polygamma!1(GDN!1(2, 3));
    assert(a_act is a_exp, format("Î¨â‚(<2,3>) = %s != %s", a_act, a_exp));

    assert(polygamma!1(GDN!1(+0.)) is GDN!1(real.infinity, -real.infinity));
    assert(polygamma!1(GDN!1(-0.)) is GDN!1(real.infinity, real.infinity));

    const u = polygamma!1(GDN!1(-1));
    assert(u == real.infinity && isNaN(u.d));

    const w = polygamma!1(GDN!1(real.nan));
    assert(isNaN(w.val) && isNaN(w.d));

    const e = polygamma!1(GDN!1(1, real.nan));
    assert(e == ad.math.polygamma.polygamma!1(1) && isNaN(e.d));

    assert(polygamma!1(GDN!1(real.infinity)) == GDN!1(+0., -0.));

    const t = polygamma!1(GDN!1(-real.infinity));
    assert(t.val == real.infinity && isNaN(t.d), format("Î¨â‚(-âˆ) != %s", t));

    const y = GDN!1(ad.math.polygamma.polygamma!2(2), ad.math.polygamma.polygamma!3(2));
    assert(polygamma!2(GDN!1(2)) is y);

    assert(polygamma!2(GDN!1(+0.)) is GDN!1(-real.infinity, real.infinity));
    assert(polygamma!2(GDN!1(-0.)) is GDN!1(real.infinity, real.infinity));

    const i = polygamma!2(GDN!1(-1));
    assert(isNaN(i.val) && isNaN(i.d));

    const o_exp = GDN!2(
        ad.math.polygamma.polygamma!1(3),
        ad.math.polygamma.polygamma!2(3),
        ad.math.polygamma.polygamma!3(3));
    // <f',f"> = <1,0>Î¨â‚‚(<3,1>) = <1,0><Î¨â‚‚(3),Î¨â‚ƒ(3)> = <Î¨â‚‚(3),Î¨â‚ƒ(3)>
    const o_act = polygamma!1(GDN!2(3));
    assert(o_act is o_exp, format("Î¨â‚(3) = %s != %s", o_exp, o_act));
}


/**
 * the gamma function, $(MATH Î“), of a generalized dual number
 *
 * If $(MATH f(x) = Î“(g(x))), then $(MATH f' = Î“(g)Î¨(g)g')
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN`argument
 *
 * Returns:
 *   $(MATH Î“(g)) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg gamma(ulong Deg)(in GDN!Deg g)
{
    const g_red = g.reduce();

    static if (Deg == 1) {
        const f_red = std.mathspecial.gamma(g_red);
        const psi = std.mathspecial.digamma(g_red);
    } else {
        const f_red = gamma(g_red);
        const psi = digamma(g_red);
    }

    return GDN!Deg(asReal(f_red), f_red*psi*g.d);
}

///
unittest
{
    import std.mathspecial: digamma;

    assert(gamma(GDN!1(1)) is GDN!1(1, digamma(1)));
}

unittest
{
    import std.format: format;

    assert(gamma(GDN!1(2, 3)) is GDN!1(1, 3*std.mathspecial.digamma(2)));

    const q = gamma(GDN!1(real.nan));
    assert(isNaN(q.val) && isNaN(q.d));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const t = gamma(GDN!1(-0.));
//     assert(t is GDN!1(-real.infinity, -real.infinity), format("Î“(-0) = %s", t));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const y = gamma(GDN!1(+0.));
//     assert(y is GDN!1(real.infinity, -real.infinity), format("Î“(+0) = %s", y));

    const w = gamma(GDN!1(-2));
    assert(isNaN(w.val) && isNaN(w.d));

    assert(gamma(GDN!1(+real.infinity)) is GDN!1(+real.infinity, +real.infinity));

    const e = gamma(GDN!1(-real.infinity));
    assert(isNaN(e.val) && isNaN(e.d));

    const r = GDN!2(
        2,
        2*std.mathspecial.digamma(3),
        2*std.mathspecial.digamma(3)^^2 + 2*ad.math.polygamma.polygamma!1(3));
    assert(gamma(GDN!2(3)) is r);
    // <f',f"> = Î“(<3,1>)Î¨(<3,1>)<1,0>
    //         = <2,2Î¨(3)><Î¨(3),Î¨â‚(3)><1,0>
    //         = <2Î¨(3),2Î¨Â²(3)+2Î¨â‚(3)>
}


/**
 * Computes the natural logarithm of the gamma function for generalized dual number.
 *
 * If $(MATH f(x) = ln|Î“(g(x))|), $(MATH f' = Î¨(g)g').
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN` argument
 *
 * Returns:
 *   $(MATH ln|Î“(g)|) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg logGamma(ulong Deg)(in GDN!Deg g)
{
    static if (Deg == 1)
        const df = std.mathspecial.digamma(g.reduce());
    else
        const df = digamma(g.reduce());

    return GDN!Deg(std.mathspecial.logGamma(g.val), df*g.d);
}

///
unittest
{
    import std.mathspecial: digamma;

    assert(logGamma(GDN!1(2)) is GDN!1(0, digamma(2)));
}

unittest
{
    import std.format: format;
    import std.math: log;

    assert(logGamma(GDN!1(3, 4)) is GDN!1(log(2.0L), 4*std.mathspecial.digamma(3)));

    const q = logGamma(GDN!1.nan);
    assert(isNaN(q.val) && isNaN(q.d));

    const w = logGamma(GDN!1(-1));
    assert(w == real.infinity && isNaN(w.d));

    const e = logGamma(GDN!1(-real.infinity));
    assert(e == real.infinity && isNaN(e.d), format("logGamma(-inf) != %s", e));

    assert(logGamma(GDN!1(real.infinity)) is GDN!1(real.infinity, real.infinity));

    const r = GDN!2(log(24.0L), std.mathspecial.digamma(5), ad.math.polygamma.polygamma!1(5));
    assert(logGamma(GDN!2(5)) == r);
    // <f',f"> = Î¨(<5,1>)*<1,0> = <Î¨(5), Î¨â‚(5)>
}


/**
 * Computes the sign of the gamma function of a generalized dual number.
 *
 * If $(MATH f(x) = sgn(Î“(g(x)))), then $(MATH f' = 2ğ›¿(Î“(g))Î“(g)Î¨(g)g'). Since $(MATH Î“(g) â‰  0, âˆ€g),
 * $(MATH f' = 0), if it exists. It doesn't exists when $(MATH g) is a non-positive integer or
 * $(MATH -âˆ) or when $(MATH g') is infinite.
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN` argument
 *
 * Returns:
 *   It returns $(MATH  sgn(Î“(g))) as a `GDN`.
 */
pure nothrow @nogc @safe GDN!Deg sgnGamma(ulong Deg)(in GDN!Deg g)
{
    const f = std.mathspecial.sgnGamma(g.val);

    real df;

    if (!isNaN(f)) {
        if (g.val < 0) {
            ulong ngz = cast(ulong) trunc(-g.val);
            if (ngz != -g.val) df = (ngz & 1) == 0 ? -0. : +0.;
        } else if (g.val is -0.0L) {
            df = -0.;
        } else {
            df = +0.;
        }
    }

    return GDN!Deg(f, df * g.d);
}

///
unittest
{
    const f = sgnGamma(GDN!1(1));
    assert(f == 1 && f.d == 0);
}

unittest
{
// NB: This fails because of https://github.com/dlang/phobos/issues/10801, fixed in stable
//     const g = sgnGamma(GDN!1(-0.5));
//     assert(g == -1 && g.d == 0);

    const h = sgnGamma(GDN!1(+0.));
    assert(h is GDN!1(1, 0));

    const i = sgnGamma(GDN!1(-1));
    assert(isNaN(i.val) && isNaN(i.d));
}


/**
 * Computes the beta function where at least one of the arguments is a generalized dual number.
 *
 * If $(MATH f(x) = B(g(x),h(x))), then $(MATH f' = $(SUP âˆ‚B)/$(SUB âˆ‚g)g' + $(SUP âˆ‚B)/$(SUB âˆ‚h)h').
 * $(MATH $(SUP âˆ‚B(x$(SUB 1),x$(SUB 2)))/$(SUB âˆ‚x$(SUB i)) = Bâ‹…[Î¨(x$(SUB i)) - Î¨(x$(SUB 1)+x$(SUB 2))]),
 * so $(MATH f' = Bâ‹…[Î¨(g) - Î¨(g+h)]g' + Bâ‹…[Î¨(h) - Î¨(g+h)]h'). This reduces to
 * $(MATH f' = B(g,h)[Î¨(g)g' + Î¨(h)h' - Î¨(g+h)(g' + h')]).
 *
 * Params:
 *   G = the first `GDN` argument
 *   H = the second `GDN` argument
 *   g = the first `GDN` argument
 *   h = the second `GDN` argument
 *
 * Returns:
 *   $(MATH B(g,h)) as a `GDN`.
 */
pure nothrow @nogc @safe
CommonGDN!(G, H) beta(G, H)(in G g, in H h) if (isOne!(isGDN, G, H) && areAll!(isGDNOrReal, G, H))
{
    alias Deg = typeof(return).DEGREE;

    static if (Deg == 1) {
        alias B = std.mathspecial.beta;
        alias psi = std.mathspecial.digamma;
    } else {
        alias B = beta;
        alias psi = digamma;
    }

    const gg = asGDN!Deg(g);
    const hh = asGDN!Deg(h);

    const g_red = gg.reduce();
    const h_red = hh.reduce();
    const f_red = B(g_red, h_red);

    return GDN!Deg(
        asReal(f_red), f_red*(gg.d*psi(g_red) + hh.d*psi(h_red) - (gg.d + hh.d)*psi(g_red+h_red)));
}

///
unittest
{
    assert(beta(GDN!1(2), GDN!1(1)) == GDN!1(0.5, -1));
}

unittest
{
    // f' = B(g,h)[g'Î¨(g) + h'Î¨(h) - (g' + h')Î¨(g+h)]
    // In the following m and n are positive integers
    // B(m,n) = (m - 1)!(n - 1)!/(m + n - 1)!
    // Î¨(n) = -Î³ + âˆ‘áµ¢â‚Œâ‚â¿â»Â¹(1/i)
    // Î¨â‚(n) = ğœ‹Â²/6 - âˆ‘áµ¢â‚Œâ‚â¿â»Â¹(1/iÂ²)

    import std.format: format;
    import std.math: isClose;

    assert(beta(GDN!1(1), 2) is GDN!1(0.5, -0.75));

    const q_act = beta(3, GDN!1(4));
    const q_exp = GDN!1(1.0L/60, -37.0L/3_600);
    assert(q_act == q_exp);
    assert(isClose(q_act.d, q_exp.d), format("B'(3, <4,1>) = %s != %s", q_act.d, q_exp.d));
    // f = 2!3!/6! = 2/(6*5*4) = 1/(5*4*3) = 1/60
    // f' = 1/60[Î¨(4) - Î¨(7)]
    //    = [(1 + 1/2 + 1/3) - (1 + 1/2 + 1/3 + 1/4 + 1/5 + 1/6)]/60
    //    = -(1/4 + 1/5 + 1/6)/60
    //    = -(9/20 + 1/6)/60
    //    = -(27/60 + 10/60)/60
    //    = -37/60/60
    //    = -37/3600

    assert(beta(GDN!2(2), GDN!2(3)) == GDN!2(1.0L/12, -5.0L/36, 9.0L/32));
    // f = 1!2!/4! = 2/(4*3*2) = 1/12;
    // <f',f"> = B(<2,1>, <3,1>)[<1,0>Î¨(<2,1>) + <1,0>Î¨(<3,1>) - (<1,0> + <1,0>)Î¨(<2,1>+<3,1>)]
    // B(<2,1>, <3,1>) = <B(2,3), B(2,3)[Î¨(2) + Î¨(3) - (1 + 1)Î¨(2+3)]>
    //    = <B(2,3), B(2,3)[Î¨(2) + Î¨(3) - 2Î¨(5)]>
    // B(2,3) = 1/12
    // Î¨(<n,1>) = <Î¨(n),Î¨â‚(n)>
    // <1,0>Î¨(<2,1>) + <1,0>Î¨(<3,1>) - (<1,0> + <1,0>)Î¨(<2,1>+<3,1>)
    //    = <Î¨(2),Î¨â‚(2)> + <Î¨(3),Î¨â‚(3)> - <2,0><Î¨(5),Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3), Î¨â‚(2) + Î¨â‚(3)> - 2<Î¨(5),Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3), Î¨â‚(2) + Î¨â‚(3)> - <2Î¨(5),2Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3) - 2Î¨(5), Î¨â‚(2) + Î¨â‚(3) - 2Î¨â‚(5)>
    // Î¨(2) + Î¨(3) - 2Î¨(5) = -Î³ + 1 + -Î³ + 1 + 1/2 - 2(-Î³ + 1 + 1/2 + 1/3 + 1/4)
    //    = -2Î³ + 2 + 1/2 - 2Î³ - 2 - 1 - 2/3 - 1/2 = -1 - 2/3
    //    = -5/3
    // Î¨â‚(2) + Î¨â‚(3) - 2Î¨â‚(5) = ğœ‹Â²/6 - 1 + ğœ‹Â²/6 - (1 + 1/4) - 2[ğœ‹Â²/6 - (1 + 1/4 + 1/9 + 1/16)]
    //    = ğœ‹Â²/3 - 1 - 1 - 1/4 - 2(ğœ‹Â²/6 - 1 - 1/4 - 1/9 - 1/16)
    //    = ğœ‹Â²/3 - 2 - 1/4 - ğœ‹Â²/3 + 2 + 1/2 + 2/9 + 1/8 = 1/2 - 1/4 + 1/8 + 2/9 = 1/4 + 25/72
    //    = 43/72
    // <f',f"> = <1/12,(-5/3)/12><-5/3,43/72> = <1/12,-5/36><-5/3,43,72> = <-5/36,25/108 + 43/864>
    //    = <-5/36,9/32>

    const w_act = beta(GDN!1(-0.5), GDN!1(1));
    // f = B(-0.5, 1) = Î“(-0.5)Î“(1)/Î“(0.5) = ğœ‹/[sin(-ğœ‹/2)Î“(1.5)]/Î“(0.5) = -2ğœ‹Î“(1)/[âˆšğœ‹Î“(2)âˆšğœ‹]
    //   = -2
    // f' = B(-0.5,1)[Î¨(-0.5) + Î¨(1) - 2Î¨(0.5)]
    // Î¨(1.5) - Î¨(-0.5) = ğœ‹â‹…cot(-ğœ‹/2) =>
    // Î¨(-0.5) = Î¨(1.5) = -Î³ - 2ln(2) + âˆ‘áµ¢â‚Œâ‚Â¹2/(2i - 1)
    //         = 2 - Î³ - 2ln(2)
    // Î¨(0.5) = -Î³ - 2ln(2)
    // Î¨(1) = -Î³
    // f' = -2{2 - Î³ - 2ln(2) + -Î³ - 2[-Î³ - 2ln(2)]} = -2[2 - 2Î³ - 2ln(2) + 2Î³ + 4ln(2)]
    //    = -4 - 4ln(2)
    const w_exp = GDN!1(-2, -4-4*LN2);
    assert(
        isClose(w_act.val, w_exp.val) && isClose(w_act.d, w_exp.d),
        format("B(-.5,1) = %s != %s", w_act, w_exp));

    const e = beta(GDN!1(-1), GDN!1(2));
    assert(isNaN(e.val) && isNaN(e.d));
    // f = B(-1, 2) = Î“(-1)Î“(1)/Î“(1) = Î“(-1), DNE

    const r = beta(GDN!1(-0.5), GDN!1(0.5));
    assert(r.val is -0. && isNaN(r.d));
    // f = B(-.5, .5) = Î“(-.5)Î“(.5)/Î“(0) =
    //   = -0
    // f' = B(-.5, .5)[Î¨(-.5) + Î¨(.5) - 2Î¨(0)]

    const t = beta(GDN!1(-1), GDN!1(1));
    assert(isNaN(t.val) && isNaN(t.d), format("B(-1,1) != %s", t));
    // lim(x->1) B(-x,x) = lim(x->1) Î“(-x)Î“(x)/Î“(0) = 0
    // lim(x->1) B(-x,1) = lim(x->1) Î“(-x)Î“(1)/Î“(1 - x) = lim(x->1) Î“(-x)/Î“(1 - x)
    // lim(x->1) ğœ‹/[sin(-ğœ‹x)Î“(1+x)]/{ğœ‹/[sin(ğœ‹x)Î“(x)]} = lim(x->1) ğœ‹sin(ğœ‹x)Î“(x)/[ğœ‹sin(-ğœ‹x)Î“(1+x)]
    // lim(x->1) sin(ğœ‹x)Î“(x)/[-sin(ğœ‹x)Î“(1+x)] = lim(x->1) -Î“(x)/Î“(1+x) = -Î“(1)/Î“(2) = -1
    // B(-1,1) DNE

    assert(beta(GDN!1(2), GDN!1(-1.5)) is beta(GDN!1(-1.5), GDN!1(2)));

    const y = std.mathspecial.beta(-0.5, -0.2);
    const u = std.mathspecial.digamma(-0.5);
    const i = std.mathspecial.digamma(-0.2);
    const o = std.mathspecial.digamma(-0.7);
    assert(beta(GDN!1(-0.5), GDN!1(-0.2)) is GDN!1(y, y*(u + i - 2*o)));
    // f = B(-.5,-.2)
    // f' = B(-.5,-.2)[Î¨(-.5) + Î¨(-.2) - 2Î¨(-.7)]

// NB: Fails because std.mathspecial.beta(-1.5, -.5) is NaN. Fixed in stable branch.
    // const p = beta(GDN!1(-1.5), GDN!1(-0.5));
    // // f = B(-1.5,-.5) = Î“(-1.5)Î“(-.5)/Î“(-2) = -0
    // // f' = f[Î¨(-1.5) + Î¨(-.5) - 2Î¨(-2)] DNE
    // assert(p.val is -0.0L && isNaN(p.d), format("B(-1.5,-0.5) != %s", p));

    const a = beta(GDN!1(-1), GDN!1(-0.5));
    assert(isNaN(a.val) && isNaN(a.d));
    // f = B(-1, -0.5) = Î“(-1)Î“(-.5)/Î“(-1.5) DNE

    const s = beta(GDN!1(-1), GDN!1(-2));
    assert(isNaN(s.val) && isNaN(s.d));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const d = beta(GDN!1(-0.), GDN!1(-0.5));
//     // f' = B(-0, -0.5)[Î¨(-0) + Î¨(-0.5) - 2Î¨(-0.5)] = -âˆ[âˆ - Î¨(-0.5)] = -âˆ,
//     assert(d == -real.infinity && d.d == -real.infinity, format("B(-0,-.5) != %s", d));

    const f = beta(GDN!1(-0.), GDN!1(-1));
    assert(isNaN(f.val) && isNaN(f.d));

// NB: Fails because std.mathspecial.digamma(+0.) is NaN. Fixed in stable branch.
//     const g = beta(GDN!1(+0.), GDN!1(-1.5));
//     // f' = B(+0, -1.5)[Î¨(+0) + Î¨(-1.5) - 2Î¨(-1.5)] = âˆ[-âˆ - Î¨(-0.5)] = -âˆ,
//     assert(g == real.infinity && g.d == -real.infinity, format("B(+0, -1.5) = %s", g));

    const h = beta(GDN!1(+0.), GDN!1(-2));
    assert(isNaN(h.val) && isNaN(h.d));

// NB: Fails because std.mathspecial.digamma(+0.) = real.nan. Fixed in stable.
//    const j = beta(GDN!1(+0.), GDN!1(+0.));
//    assert(j == real.infinity && isNaN(j.d), format("B(+0,+0) = %s", j));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const k = beta(GDN!1(-0.), GDN!1(1));
//     // f' = B(-0, 1)[Î¨(-0) + Î¨(1) - 2Î¨(1)] = -âˆ[âˆ - Î¨(1)] = -âˆ,
//     assert(k == -real.infinity && k.d == -real.infinity, format("B(-0, 1) = %s", k));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const l = beta(GDN!1(+0.), GDN!1(1));
//     // f' = B(+0, 1)[Î¨(+0) + Î¨(1) - 2Î¨(1)] = âˆ[-âˆ - Î¨(1)] = -âˆ,
//     assert(l == real.infinity && l.d == -real.infinity, format("B(+0,1) = %s", l));

// NB: Fails because std.mathspecial.beta(real.infinity, 1) is NaN. Fixed in
// stable branch.
//     const z = beta(GDN!1(real.infinity), GDN!1(1));
//     assert(z == 0 && isNaN(z.d), format("B(âˆ,1) != %s", z));

// NB: Fails because std.mathspecial.beta(real.infinity, real.infinity) is NaN.
// Fixed in stable branch.
//     const x = beta(GDN!1(real.infinity), GDN!1(real.infinity));
//     assert(x == 0 && isNaN(x.d), format("B(âˆ,âˆ) = %s", x));

    const c = beta(GDN!1(-real.infinity), GDN!1(1));
    assert(isNaN(c.val) && isNaN(c.d));

    const v = beta(GDN!1.nan, GDN!1(1));
    assert(isNaN(v.val) && isNaN(v.d));
}


/**
 * the digamma function,$(MATH Î¨), of a generalized dual number
 *
 * If $(MATH f(x) = Î¨(g(x))), then $(MATH f' = Î¨â‚(g)g'), where $(MATH Î¨â‚) is the polygamma function
 * of order $(MATH 1) (trigamma function).
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN`argument
 *
 * Returns:
 *   $(MATH Î¨(g)) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg digamma(ulong Deg)(in GDN!Deg g)
{
    static if (Deg == 1)
        const df = ad.math.polygamma.polygamma!1(g.reduce());
    else
        const df = polygamma!1(g.reduce());

    return GDN!Deg(std.mathspecial.digamma(g.val), df*g.d);
}

///
unittest
{
    import ad.math.internal: isNaN;

    const trigamma_1 = PI^^2 / 6;  // Î¨â‚(1)

    const f_act = digamma(GDN!1(1));
    const f_exp = GDN!1(std.mathspecial.digamma(1), trigamma_1);
    assert(f_act == f_exp);

    assert(isNaN(digamma(GDN!1(-1))));
    assert(digamma(GDN!1(real.infinity)) is GDN!1(real.infinity, +0.));
}

unittest
{
    import std.format: format;

    const e = GDN!1(std.mathspecial.digamma(2), 3*ad.math.polygamma.polygamma!1(2));
    assert(digamma(GDN!1(2, 3)) is e);

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const f_nz = digamma(GDN!1(-0.));
//     assert(f_nz is GDN!1(real.infinity, real.infinity), format("Î¨â‚(-0) = %s", f_nz));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const f_pz = digamma(GDN!1(+0.));
//     assert(f_pz is GDN!1(-real.infinity, real.infinity), format("Î¨â‚(+0) = %s", f_pz));

    const q = digamma(GDN!1(-real.infinity));
    assert(isNaN(q.val) && isNaN(q.d));

    const g = digamma(GDN!1(real.nan));
    assert(isNaN(g.val) && isNaN(g.d));

    const w = GDN!2(
        std.mathspecial.digamma(2),
        ad.math.polygamma.polygamma!1(2),
        ad.math.polygamma.polygamma!2(2));

    assert(digamma(GDN!2(2)) is w);
}


/**
 * The natural logarithm of a `GDN` minus digamma of the same `GDN`.
 *
 * If $(MATH f(x) = ln(g(x)) - Î¨(g(x))), then $(MATH f' = [1/g - Î¨â‚(g)]g'), where $(MATH Î¨â‚) is the
 * polygamma function of order one (trigamma function).
 *
 * Params:
 *   Deg = the degree of g
 *   g = the argument, must be positive
 *
 * Returns:
 *   a GDN representing the natural logarithm of g minus digamma of g.
 */
pure nothrow @nogc @safe GDN!Deg logmdigamma(ulong Deg)(in GDN!Deg g)
in(signbit(g) == 0, "the argument must be positive")
do {
    const f = std.mathspecial.logmdigamma(g.val);
    const g_red = g.reduce();

    static if (Deg == 1)
        const dfdg = 1/g_red - ad.math.polygamma.polygamma!1(g_red);
    else
        const dfdg = g_red.inv() - polygamma!1(g_red);

    return GDN!Deg(f, dfdg*g.d);
}

///
unittest
{
    import std.math: isClose;

    // Euler-Mascheroni constant
    const Î³ = 0.577_215_664_901_532_860_607L;

    const f_act = logmdigamma(GDN!1(1));
    const f_exp = GDN!1(Î³, 1-PI^^2/6);
    assert(isClose(f_act.val, f_exp.val) && isClose(f_act.d, f_exp.d));
}

unittest
{
    import std.format: format;
    import std.math: isClose;
    import ad.math: log;

    const w = logmdigamma(GDN!1(+0.));
    assert(w == real.infinity && isNaN(w.d), format("logmdigamma(+0) != %s", w));
    // ln(x) - 1/x â‰¤ Î¨(x) â‰¤ ln(x) - 1/(2x), x>0
    // -1/x â‰¤ Î¨(x) - ln(x) â‰¤ -1/(2x)
    // 1/x â‰¥ ln(x) - Î¨(x) â‰¥ 1/(2x)
    // lim{xâŸ¶0âº} ln(x) - Î¨(x) â‰¥ lim{xâŸ¶0âº} 1/(2x) = +âˆ

    const e_act = logmdigamma(GDN!2(2));
    const e_exp = log(GDN!2(2)) - digamma(GDN!2(2));
    assert(isClose(e_act.val, e_exp.val));
    assert(isClose(e_act.d.val, e_exp.d.val, 10*real.epsilon));
    assert(e_act.d!2 == e_exp.d!2);
}


/**
 * The inverse of the function formed from the natural logarithm of a `GDN` minus digamma of the
 * same `GDN`.
 *
 * If $(MATH f(x) = ln(g(x)) - Î¨(g(x))), then $(MATH g = fâ»Â¹(f)) and $(MATH f' = [1/g - Î¨â‚(g)]g'),
 * where $(MATH Î¨â‚) is the polygamma function of order one (trigamma function). Thus
 * $(MATH g' = f' / [1/g - Î¨â‚(g)] = f'g / [1 - gÎ¨â‚(g)])
 *
 * Params:
 *   Deg = the degree of f
 *   f = the `GDN` argument
 *
 * Return:
 *   It returns `g` such that the natural logarithm of `g` minus digamma of `g` is equal to f.
 */
pure nothrow @nogc @safe GDN!Deg logmdigammaInverse(ulong Deg)(in GDN!Deg f)
{
    static if (Deg == 1) {
        alias ln_m_digamma_inv = std.mathspecial.logmdigammaInverse;
        alias trigamma = ad.math.polygamma.polygamma!1;
    } else {
        alias ln_m_digamma_inv =  logmdigammaInverse;
        alias trigamma = polygamma!(1, Deg-1);
    }

    const g_red = ln_m_digamma_inv(f.reduce());

    GDN!Deg.DerivType!1 naive_derivative() { return f.d * g_red / (1 - g_red*trigamma(g_red)); }

    // Assume x > 0. (x + 1/2)/xÂ² â‰¤ Î¨â‚(x) â‰¤ (x + 1)/xÂ² â‡’ -1/x â‰¤ 1 - xÎ¨â‚(x) â‰¤ -1/(2x).
    // lim{xâŸ¶0âº} -1/x = -âˆ = lim{xâŸ¶0âº} -1/(2x) â‡’ lim{xâŸ¶0âº} 1 - xÎ¨â‚(x) = -âˆ
    // lim{xâŸ¶+âˆ} -1/x = 0â» = lim{xâŸ¶+âˆ} -1/(2x) â‡’ lim{xâŸ¶+âˆ} 1 - xÎ¨â‚(x) = 0â»
    GDN!Deg.DerivType!1 dg;

    if (g_red == 0 && signbit(g_red) == 0) {
        static if (Deg == 1)
            dg = -0. * f.d;
        else
            dg = GDN!Deg.DerivType!1(asReal(g_red.d), naive_derivative().d);
    } else if (g_red == real.infinity) {
        static if (Deg == 1)
            dg = -real.infinity * f.d;
        else
            dg = GDN!Deg.DerivType!1(asReal(g_red.d), naive_derivative().d);
    } else {
        dg = naive_derivative();
    }

    return GDN!Deg(asReal(g_red), dg);
}

///
unittest
{
    import std.math: isClose;

    const f = GDN!1(1);
    const g = logmdigammaInverse(logmdigamma(f));
    assert(isClose(g.val, f.val) && isClose(g.d, f.d));
}

unittest
{
    import std.format: format;
    import std.math: isClose;

    const Î³ = 0.577_215_664_901_532_860_607L;
    const Î¶3 = 1.202_056_903_159_594_285_400L;

    const q = logmdigammaInverse(GDN!1(+0.0L));
    // g = +âˆ
    // g' = lim{gâŸ¶+âˆ} 1/[1/g - Î¨â‚(g)] = lim{gâŸ¶0âº} 1/g - Î¨â‚(g) = -lim{gâŸ¶0âº} Î¨â‚(g) - 1/g
    //    â‰¤ -lim{gâŸ¶0âº} (g + 1)/gÂ² - 1/g = -lim{gâŸ¶0âº} 1/gÂ² = -âˆ
    // g' = -âˆ
    assert(q is GDN!1(real.infinity, -real.infinity), format("logmdigammaInverse(+0) != %s", q));

    const w = logmdigammaInverse(GDN!1(real.infinity));
    // lim{gâŸ¶0âº} Î¨(g) ~ lim{gâŸ¶0âº} ln(g) - 1/(2g) â‡’ lim{gâŸ¶0âº} Î¨(g) - ln(q) ~ lim{gâŸ¶0âº} -1/(2g)
    // â‡’ lim{gâŸ¶0âº} ln(g) - Î¨(g) ~ lim{gâŸ¶0âº} 1/(2g) = +âˆ
    // â‡’ g = 0âº
    // g' = lim{gâŸ¶0âº} 1/[1/g - Î¨â‚(g)] = lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) = -lim{gâŸ¶+âˆ} Î¨â‚(g) - 1/g
    // -lim{gâŸ¶+âˆ} (g + 1/2)/gÂ² - 1/g â‰¤ -lim{gâŸ¶+âˆ} Î¨â‚(g) - 1/g â‰¤ -lim{gâŸ¶+âˆ} (g + 1)/gÂ² - 1/g
    // â‡’ lim{gâŸ¶+âˆ} 1/g - (g + 1/2)/gÂ² â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ lim{gâŸ¶+âˆ} 1/g - (g + 1)/gÂ²
    // â‡’ lim{gâŸ¶+âˆ} -1/(2gÂ²) â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ lim{gâŸ¶+âˆ} -1/gÂ²
    // â‡’ 0â» â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ 0â» â‡’ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) = 0â»
    // g' = 0â»
    assert(w is GDN!1(+0., -0.), format("logmdigammaInverse(âˆ) != %s", w));

// NB: Failed because std.mathspecial.logmdigammaInverse(-.0) is -real.infinity, fixed in stable
//     const e = logmdigammaInverse(GDN!1(-0.));
//     // Assume x > 0. ln(x) - 1/(2x) - Î¨â‚(x) > 0 â‡’ ln(x) - Î¨â‚(x) > 1/(2x) > 0.
//     // f(x) = ln(x) - Î¨â‚(x) is defined for x âˆŠ â„âº. f: â„âº â†¦ â„âº â‡’ fâ»Â¹: â„âº â†¦ â„âº
//     // f(-0) DNE,
//     assert(isNaN(e), format("logmdigammaInverse(-0) = %s", e));

    const r = logmdigammaInverse(GDN!1(-1));
    assert(isNaN(r.val) && isNaN(r.d));

    const t = GDN!2(Î³);
    const u = logmdigammaInverse(t);
    // ln(1) - Î¨(1) == Î³ â‡’ g = 1
    // g' = f'g/[1 - gÎ¨â‚(g)] = 1*1/[1 - 1*Î¨â‚(1)] = 1/(1 - ğœ‹Â²/6) = 6/(6 - ğœ‹Â²)
    // <g',g"> = <f',f"><g,g'>/[1 - <g,g'>Î¨â‚(<g,g'>)]
    //         = <1,0><1,g'>/[1 - <1,g'>Î¨â‚(<1,g'>)]
    //         = <1,g'>/[1 - <1,g'><Î¨â‚(1),Î¨â‚‚(1)g'>]
    // Î¨â‚‚(1) = (-1)Â³2!Î¶(3,1) = -2Î¶(3)
    // <g',g"> = <1,g'>/[1 - <1,g'><ğœ‹Â²/6,-2Î¶(3)g'>]
    //         = <1,g'>/[1 - <ğœ‹Â²/6,ğœ‹Â²g'/6 - 2Î¶(3)g'>]
    //         = <1,g'>/<1 - ğœ‹Â²/6,[2Î¶(3) - ğœ‹Â²/6]g'>
    //         = <1/(1 - ğœ‹Â²/6),{(1 - ğœ‹Â²/6)g' - [2Î¶(3) - ğœ‹Â²/6]g'}/(1 - ğœ‹Â²/6)Â²>
    //         = <g',[1 - 2Î¶(3)](g')Â³>
    // g" = [1 - 2Î¶(3)](g')Â³ = [1 - 2Î¶(3)][6/(6 - ğœ‹Â²)]Â³
    // g" = 216[1 - 2Î¶(3)]/(6 - ğœ‹Â²)Â³
    const dg = 6/(6 - PI^^2);
    const d2g = 216*(1 - 2*Î¶3)/(6 - PI^^2)^^3;
    assert(
        isClose(u.val, 1) && isClose(u.d.val, dg) && isClose(u.d!2, d2g),
        format("logmdigammaInverse(Î³) != %s", u));
}


/* This function computes the derivative of I(g(x); a,b) with respect to x,
 * where a and b are constants.
 *
 * Let f(x) = I(g(x); a,b). Then f' = dI/dgâ‹…g' = g'(d/dg)B(g; a,b)/B(a,b)) where
 * B(g; a,b) = âˆ«â‚€áµtáµƒâ»Â¹(1-t)áµ‡â»Â¹dt is the incomplete beta function.
 *
 * The integrand táµƒâ»Â¹(1-t)áµ‡â»Â¹ is Lebesgue integrable over 0 â‰¤ t â‰¤ 1. Therefore,
 * B' = gáµƒâ»Â¹(1-g)áµ‡â»Â¹ almost everywhere. g = 0 and 1 are the only values where
 * gáµƒâ»Â¹(1-g)áµ‡â»Â¹ doesn't exist for every a and b, but the one-sided limits do.
 * This algorithm defines
 * B'(0; a,b) = lim{gâ†’0âº} gáµƒâ»Â¹(1-g)áµ‡â»Â¹ = { âˆ, 0<a<1; 1, a=1; 0, a>1 }), and
 * B'(1; a,b) = lim{gâ†’1â»} gáµƒâ»Â¹(1-g)áµ‡â»Â¹ = { âˆ, 0<b<1; 1, b=1; 0, b>1 }).
 *
 * Thus I'(g; a,b) has the following form when 0 < a,b < âˆ.
 *
 *    - I'(g; a,b) = gáµƒâ»Â¹(1-g)áµ‡â»Â¹/B(a,b), 0 < g < 1
 *    - I'(0; a,b) = { âˆ, a<1; 1/B(1,b), a=1; 0, a>1 }
 *    - I'(1; a,b) = { âˆ, b<1; 1/B(a,1), b=1; 0, b>1 }
 *
 * Here are the degenerate cases of I'. Let H(x) = { 0, x<0; 1, xâ‰¥0 } be the
 * Heaviside step function in the following.
 *
 *    - I'(g; 0,b) = (d/dg)lim{aâ†’0âº} I(g; a,b) = (d/dg)(1 - H(-g)) = ğ›¿(g)
 *    - I'(g; âˆ,b) = (d/dg)lim{aâ†’âˆ} I(g; a,b) = (d/dg)H(g-1) = ğ›¿(g-1)
 *    - I'(g; a,0) = (d/dg)lim{bâ†’0âº} I(g; a,b) = (d/dg)H(g-1) = ğ›¿(g-1)
 *    - I'(g; a,âˆ) = (d/dg)lim{bâ†’âˆ} I(g; a,b) = (d/dg)[1 - H(-g)] = ğ›¿(g)
 *    - I'(g; 0,0) = (d/dg)lim{a,bâ†’0âº} I(g; a,b), does not exist
 *    - I'(g; 0,âˆ) = (d/dg)lim{aâ†’0âº,bâ†’âˆ) I(g; a,b) = (d/dg)[1 - H(-g)] = ğ›¿(g)
 *    - I'(g; âˆ,0) = (d/dg)lim{aâ†’âˆ,bâ†’0âº} I(g; a,b) = (d/dg)H(g-1) = ğ›¿(g-1)
 *    - I'(g; âˆ,âˆ) = (d/dg)lim{a,bâ†’âˆ) I(g; a,b), does not exist
 */
private pure nothrow @nogc @safe
GDN!Deg.DerivType!1 betaIncompleteDeriv(ulong Deg)(in real a, in real b, in GDN!Deg g)
{
    alias Deriv = typeof(return);

    Deriv dfdg;
    if ((a == 0 && b == 0) || (a == real.infinity && b == real.infinity)) {
        dfdg = Deriv.nan;
    } else if (a == 0 || b == real.infinity) {
        dfdg = dirac(g.reduce());
    } else if (a == real.infinity || b == 0) {
        dfdg = dirac(g.reduce() - 1);
    } else {
        const g_red = g.reduce();
        const numerator = g_red^^(a - 1) * (1 - g_red)^^(b - 1);
        const denominator = std.mathspecial.beta(a, b);
        if (numerator == real.infinity && denominator == real.infinity) {
            // In this case, the denominator is not really infinite. It's just
            // larger than real.max. Instead of returning âˆ/âˆ = NaN, return the
            // numerator. The derivatives still need to be scaled.
            static if (Deg == 1)
                dfdg = numerator;
            else
                dfdg = Deriv(numerator.val, numerator.d/denominator);
        } else if (numerator == 0 && denominator == 0) {
            // In this case, the denominator is not really zero. It's just too
            // small to represent. Instead of return 0/0 = NaN, return the
            // numerator. The derivatives still need to be scaled.
            static if (Deg == 1)
                dfdg = numerator;
            else
                dfdg = Deriv(numerator.val, numerator.d/denominator);
        } else {
            dfdg = numerator / denominator;
        }
    }

    return dfdg * g.d;
}

unittest
{
    import std.format: format;

    //
    // Tests of degree 1
    //

    // a = +0, b = +0

    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(0))));
    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(.5))));
    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(1))));

    // a = +0, b = .5

    assert(betaIncompleteDeriv(+0., .5, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., .5, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., .5, GDN!1(1)) == 0);

    // a = +0, b = 1

    assert(betaIncompleteDeriv(+0., 1, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., 1, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., 1, GDN!1(1)) == 0);

    // a = +0, b = 2

    assert(betaIncompleteDeriv(+0., 2, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., 2, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., 2, GDN!1(1)) == 0);

    // a = +0, b = âˆ

    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(1)) == 0);

    // a = .5, b = +0

    assert(betaIncompleteDeriv(.5, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(.5, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(.5, +0., GDN!1(1)) is real.infinity);

    // a = .5, b = .5

    assert(betaIncompleteDeriv(.5, .5, GDN!1(0)) is real.infinity);

    assert(isClose(betaIncompleteDeriv(.5, .5, GDN!1(.5)), M_2_PI));
    // f' = .5^-.5*.5^-.5/B(.5,.5) = 2Î“(1)/[Î“(.5)Î“(.5)] = 2/(âˆšğœ‹âˆšğœ‹) = 2/ğœ‹

    assert(betaIncompleteDeriv(.5, .5, GDN!1(1)) is real.infinity);

    // a = .5, b = 1

    assert(betaIncompleteDeriv(.5, 1, GDN!1(0)) is real.infinity);

    assert(betaIncompleteDeriv(.5, 1, GDN!1(.5)) == SQRT1_2);
    // f' = .5^-.5/B(.5,1) = âˆš2*Î“(1.5)/[Î“(.5)Î“(1)] = âˆš2(âˆšğœ‹/2)/âˆšğœ‹ = âˆš2/2

    assert(betaIncompleteDeriv(.5, 1, GDN!1(1)) == .5);
    // f' = 1/B(.5,1) = Î“(1.5)/[Î“(.5)Î“(1)] = (âˆšğœ‹/2)/âˆšğœ‹ = 1/2

    // a = .5, b = 2

    assert(betaIncompleteDeriv(.5, 2, GDN!1(0)) is real.infinity);

    assert(betaIncompleteDeriv(.5, 2, GDN!1(.5)) == 3*SQRT2/8);
    // f' = .5^-.5*.5/B(.5,2) = (âˆš2/2)Î“(5/2)/[Î“(1/2)Î“(2)] = âˆš2(3âˆšğœ‹/4)/(2âˆšğœ‹) = 3âˆš2/8

    assert(betaIncompleteDeriv(.5, 2, GDN!1(1)) == 0);

    // a = .5, b = âˆ

    assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(.5)) == 0);
        assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(1)) == 0);

    // a = 1, b = +0

    assert(betaIncompleteDeriv(1, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(1, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(1, +0., GDN!1(1)) is real.infinity);

    // a = 1, b = .5

    assert(betaIncompleteDeriv(1, .5, GDN!1(0)) == 0.5L);

    const zhh = betaIncompleteDeriv(1, .5, GDN!1(.5));
    // f' = .5^-.5/B(1,.5) = âˆš2/2
    assert(zhh == SQRT1_2, format("I'(.5; 1,.5) = %s", zhh));

    assert(betaIncompleteDeriv(1, .5, GDN!1(1)) is real.infinity);

    // a = 1, b = 1

    assert(betaIncompleteDeriv(1, 1, GDN!1(0)) == 1);

    const ooh = betaIncompleteDeriv(1, 1, GDN!1(0.5L));
    assert(ooh == 1, format("I'(.5; 1,1) = %s", ooh));

    assert(betaIncompleteDeriv(1, 1, GDN!1(1)) == 1);

    // a = 1, b = 2

    assert(betaIncompleteDeriv(1, 2, GDN!1(0)) == 2);
    // f' = 1/B(1,2) = Î“(3)/[Î“(1)Î“(2)] = 2

    assert(betaIncompleteDeriv(1, 2, GDN!1(.5)) == 1);
    // f' = .5/B(1,2) = Î“(3)/[2Î“(1)Î“(2)] = 2/2 = 1

    assert(betaIncompleteDeriv(1, 2, GDN!1(1)) == 0);

    // a = 1, b = âˆ

    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(1)) == 0);

    // a = 2, b = +0

    assert(betaIncompleteDeriv(2, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(2, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(2, +0., GDN!1(1)) is real.infinity);

    // a = 2, b = .5

    assert(betaIncompleteDeriv(2, .5, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, .5, GDN!1(.5)) == 3*SQRT2/8);
    // f' = .5*.5^-.5/B(2,.5) = (âˆš2/2)Î“(5/2)/[Î“(1/2)Î“(2)] = 3âˆš2/8

    assert(betaIncompleteDeriv(2, .5, GDN!1(1)) is real.infinity);

    // a = 2, b = 1

    assert(betaIncompleteDeriv(2, 1, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, 1, GDN!1(0.5)) == 1);
    // f' = .5^2/B(2,1) = 1

    assert(betaIncompleteDeriv(2, 1, GDN!1(1)) == 2);

    // a = 2, b = 2

    assert(betaIncompleteDeriv(2, 2, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, 2, GDN!1(0.5)) == 1.5L);
    // f' = .5*.5/B(2,2) = Î“(4)/[4Î“(2)Î“(2)] = 6/4 = 3/2

    assert(betaIncompleteDeriv(2, 2, GDN!1(1)) == 0);

    // a = 2, b = âˆ

    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(1)) == 0);

    // a = âˆ, b = +0

    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(1)) is real.infinity);

    // a = âˆ, b = .5

    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(1)) is real.infinity);

    // a = âˆ, b = 1

    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(1)) is real.infinity);

    // a = âˆ, b = 2

    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(1)) is real.infinity);

    // a = âˆ, b = âˆ

    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(0))));
    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(.5))));
    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(1))));

    // g' â‰  1

    assert(betaIncompleteDeriv(2, 2, GDN!1(0.5, 0.5)) == 0.75L);
    // from earlier test: I'(GDN!1(0.5); 2,2) = 1.5 when g' = 1 so when g' = 0.5
    // expect I' = 1.5 * 0.5 = 0.75

    //
    // Tests of degree 2
    //

    // a = 0âº

    assert(betaIncompleteDeriv(+0., 1, GDN!2(.5)) is GDN!1(0, 0));

    // a = 1, b = 0âº

    assert(betaIncompleteDeriv(1, +0., GDN!2(.5)) is GDN!1(0, 0));
    // limit I(.5;1,0+) = 0 and derivative is dirac(g-1)==0

    // a = 1, b = 1

    assert(betaIncompleteDeriv(1, 1, GDN!2(0.0L)) is GDN!1(1, 0));

    assert(betaIncompleteDeriv(2, 2, GDN!2(.5)) is GDN!1(1.5, 0));
    // f' = <.5,1>^1(1 - <.5,1>)^1/B(2,2) = <.5,1><.5,-1>Î“(4)/[Î“(2)Î“(2)] = 6<.25,-.5 + .5> =<1.5,0>

    assert(betaIncompleteDeriv(1, 1, GDN!2(1.0L)) is GDN!1(1.0L, 0));

    // a = 1, b = âˆ

    assert(betaIncompleteDeriv(1, real.infinity, GDN!2(.5)) is GDN!1(0, 0));

    // a = âˆ, b = 1

    assert(betaIncompleteDeriv(real.infinity, 1, GDN!2(.5)) is GDN!1(0, 0));

    // g' â‰  1

    assert(betaIncompleteDeriv(1, 1, GDN!2(.5, 2.0L, 0.0L)) is GDN!1(2.0L, 0.0L));

    // g" â‰  0

    assert(betaIncompleteDeriv(1, 1, GDN!2(.5, 1.0L, 1.0L)) is GDN!1(1.0L, 1.0L));
}


/**
 * The regularized incomplete beta function $(MATH I$(SUB g)(a,b)).
 *
 * For fixed $(MATH a,b > 0), let $(MATH f(x) = I$(SUB g(x))(a,b)). Then
 * $(MATH f' = g'$(SUP dI$(SUB g))/$(SUB dg)).
 *
 * Params:
 *   Deg = the degree of g
 *   a = the first shape parameter, must be positive
 *   b = the second shape parameter, must be positive
 *   g = the argument, must belong to the interval $(MATH [0,1])
 *
 * Returns:
 *   the regularized incomplete beta function evaluated at g expressed as _a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg betaIncomplete(ulong Deg)(in real a, in real b, in GDN!Deg g)
in {
    assert(isNaN(a) || signbit(a) == 0, "the first shape parameter must be positive");
    assert(isNaN(b) || signbit(b) == 0, "the second shape parameter must be positive");
    assert(isNaN!Deg(g) || (g >= 0 && g <= 1), "the argument must be in [0,1]");
}
do {
    return GDN!Deg(std.mathspecial.betaIncomplete(a, b, g.val), betaIncompleteDeriv(a, b, g));
}

///
unittest
{
    assert(betaIncomplete(1, 1, GDN!1(0.5)) is GDN!1(0.5, 1));
}


// NB: In master, but not released.
// /**
//  * The regularized incomplete beta complement function $(MATH I$(SUB g)$(SUP C)(a,b)).
//  *
//  * For fixed $(MATH a,b > 0), if $(MATH f(x) = I$(SUB g(x))$(SUP C)(a,b)), then
//  * $(MATH f' = g'$(SUP dI$(SUB g)$(SUP C))/$(SUB dg)). Since
//  * $(MATH I$(SUB g)$(SUP C) = 1 - I$(SUB g)), $(MATH I$(SUB g)$(SUP C)' = -I$(SUB g)'). Thus
//  * $(MATH f' = -g'$(SUP dI$(SUB g))$(SUB dg)).
//  *
//  * Params:
//  *   Deg = the degree of g
//  *   a = the first shape parameter, must be positive
//  *   b = the second shape parameter, must be positive
//  *   g = the argument, must belong to the interval $(MATH [0,1])
//  *
//  * Returns:
//  *   the regularized incomplete beta complement function evaluated at g expressed as _a `GDN`
//  */
// pure nothrow @nogc @safe GDN!Deg betaIncompleteCompl(ulong Deg)(in real a, in real b, in GDN!Deg g)
// in {
//     assert(isNaN(a) || signbit(a) == 0, "the first shape parameter must be positive");
//     assert(isNaN(b) || signbit(b) == 0, "the second shape parameter must be positive");
//     assert(isNaN!Deg(g) || (g >= 0 && g <= 1), "the argument must be in [0,1]");
// }
// do {
//     return GDN!Deg(std.mathspecial.betaIncompleteCompl(a, b, g.val), -betaIncompleteDeriv(a, b, g));
// }
//
// ///
// unittest
// {
//     const a = 1.0L;
//     const b = 2.0L;
//     const x = GDN!1(0.1L);
//     const i = betaIncomplete(a, b, x);
//     const icc = betaIncompleteCompl(b, a, 1.0L - x);
//     assert(isClose(icc, i) && icc.d == i.d);
// }


// TODO: implement the following functions.
// betaIncompleteInverse
// gammaIncomplete
// gammaIncompleteCompl
// gammaIncompleteComplInverse
// erf
// erfc
// normalDistribution
// normalDistributionInverse
