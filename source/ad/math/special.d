/// It extends the `std.mathspecial` module to support `GDN` objects.

module ad.math.special;

public import std.mathspecial;

import std.algorithm: any;
import std.math: getNaNPayload, isInfinity, isNaN, signbit, trunc;
import std.range: only;
import std.traits: select;

static import ad.math.polygamma;

import ad.core;
import ad.math.internal:
    areAll, asGDN, asReal, CommonGDN, dirac, exp, getNaNPayload, isGDN, isGDNOrReal, isOne, isNaN,
    signbit, sgn;


private pure nothrow @nogc @safe GDN!Deg polygamma(ulong N, ulong Deg)(in GDN!Deg g) if (N > 0)
{
    static if (Deg == 1)
        const df = ad.math.polygamma.polygamma!(N+1)(g.reduce());
    else
        const df = polygamma!(N+1)(g.reduce());

    return GDN!Deg(ad.math.polygamma.polygamma!N(g.val), df*g.d);
}

unittest
{
    import std.format: format;

    const q = GDN!1(ad.math.polygamma.polygamma!1(1), ad.math.polygamma.polygamma!2(1));
    assert(polygamma!1(GDN!1(1)) is q);

    const a_exp = GDN!1(ad.math.polygamma.polygamma!1(2), 3*ad.math.polygamma.polygamma!2(2));
    const a_act = polygamma!1(GDN!1(2, 3));
    assert(a_act is a_exp, format("Î¨â‚(<2,3>) = %s != %s", a_act, a_exp));

    assert(polygamma!1(GDN!1(+0.)) is GDN!1(real.infinity, -real.infinity));
    assert(polygamma!1(GDN!1(-0.)) is GDN!1(real.infinity, real.infinity));

    const u = polygamma!1(GDN!1(-1));
    assert(u == real.infinity && isNaN(u.d));

    const w = polygamma!1(GDN!1(real.nan));
    assert(isNaN(w.val) && isNaN(w.d));

    const e = polygamma!1(GDN!1(1, real.nan));
    assert(e == ad.math.polygamma.polygamma!1(1) && isNaN(e.d));

    assert(polygamma!1(GDN!1(real.infinity)) == GDN!1(+0., -0.));

    const t = polygamma!1(GDN!1(-real.infinity));
    assert(t.val == real.infinity && isNaN(t.d), format("Î¨â‚(-âˆ) != %s", t));

    const y = GDN!1(ad.math.polygamma.polygamma!2(2), ad.math.polygamma.polygamma!3(2));
    assert(polygamma!2(GDN!1(2)) is y);

    assert(polygamma!2(GDN!1(+0.)) is GDN!1(-real.infinity, real.infinity));
    assert(polygamma!2(GDN!1(-0.)) is GDN!1(real.infinity, real.infinity));

    const i = polygamma!2(GDN!1(-1));
    assert(isNaN(i.val) && isNaN(i.d));

    const o_exp = GDN!2(
        ad.math.polygamma.polygamma!1(3),
        ad.math.polygamma.polygamma!2(3),
        ad.math.polygamma.polygamma!3(3));
    // <f',f"> = <1,0>Î¨â‚‚(<3,1>) = <1,0><Î¨â‚‚(3),Î¨â‚ƒ(3)> = <Î¨â‚‚(3),Î¨â‚ƒ(3)>
    const o_act = polygamma!1(GDN!2(3));
    assert(o_act is o_exp, format("Î¨â‚(3) = %s != %s", o_exp, o_act));
}


/**
 * the gamma function, $(MATH Î“), of a generalized dual number
 *
 * If $(MATH f(x) = Î“(g(x))), then $(MATH f' = Î“(g)Î¨(g)g')
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN`argument
 *
 * Returns:
 *   $(MATH Î“(g)) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg gamma(ulong Deg)(in GDN!Deg g)
{
    const g_red = g.reduce();

    static if (Deg == 1) {
        const f_red = std.mathspecial.gamma(g_red);
        const psi = std.mathspecial.digamma(g_red);
    } else {
        const f_red = gamma(g_red);
        const psi = digamma(g_red);
    }

    return GDN!Deg(asReal(f_red), f_red*psi*g.d);
}

///
unittest
{
    import std.mathspecial: digamma;

    assert(gamma(GDN!1(1)) is GDN!1(1, digamma(1)));
}

unittest
{
    import std.format: format;

    assert(gamma(GDN!1(2, 3)) is GDN!1(1, 3*std.mathspecial.digamma(2)));

    const q = gamma(GDN!1(real.nan));
    assert(isNaN(q.val) && isNaN(q.d));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const t = gamma(GDN!1(-0.));
//     assert(t is GDN!1(-real.infinity, -real.infinity), format("Î“(-0) = %s", t));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const y = gamma(GDN!1(+0.));
//     assert(y is GDN!1(real.infinity, -real.infinity), format("Î“(+0) = %s", y));

    const w = gamma(GDN!1(-2));
    assert(isNaN(w.val) && isNaN(w.d));

    assert(gamma(GDN!1(+real.infinity)) is GDN!1(+real.infinity, +real.infinity));

    const e = gamma(GDN!1(-real.infinity));
    assert(isNaN(e.val) && isNaN(e.d));

    const r = GDN!2(
        2,
        2*std.mathspecial.digamma(3),
        2*std.mathspecial.digamma(3)^^2 + 2*ad.math.polygamma.polygamma!1(3));
    assert(gamma(GDN!2(3)) is r);
    // <f',f"> = Î“(<3,1>)Î¨(<3,1>)<1,0>
    //         = <2,2Î¨(3)><Î¨(3),Î¨â‚(3)><1,0>
    //         = <2Î¨(3),2Î¨Â²(3)+2Î¨â‚(3)>
}


/**
 * Computes the natural logarithm of the gamma function for generalized dual number.
 *
 * If $(MATH f(x) = ln|Î“(g(x))|), $(MATH f' = Î¨(g)g').
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN` argument
 *
 * Returns:
 *   $(MATH ln|Î“(g)|) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg logGamma(ulong Deg)(in GDN!Deg g)
{
    static if (Deg == 1)
        const df = std.mathspecial.digamma(g.reduce());
    else
        const df = digamma(g.reduce());

    return GDN!Deg(std.mathspecial.logGamma(g.val), df*g.d);
}

///
unittest
{
    import std.mathspecial: digamma;

    assert(logGamma(GDN!1(2)) is GDN!1(0, digamma(2)));
}

unittest
{
    import std.format: format;
    import std.math: log;

    assert(logGamma(GDN!1(3, 4)) is GDN!1(log(2.0L), 4*std.mathspecial.digamma(3)));

    const q = logGamma(GDN!1.nan);
    assert(isNaN(q.val) && isNaN(q.d));

    const w = logGamma(GDN!1(-1));
    assert(w == real.infinity && isNaN(w.d));

    const e = logGamma(GDN!1(-real.infinity));
    assert(e == real.infinity && isNaN(e.d), format("logGamma(-inf) != %s", e));

    assert(logGamma(GDN!1(real.infinity)) is GDN!1(real.infinity, real.infinity));

    const r = GDN!2(log(24.0L), std.mathspecial.digamma(5), ad.math.polygamma.polygamma!1(5));
    assert(logGamma(GDN!2(5)) == r);
    // <f',f"> = Î¨(<5,1>)*<1,0> = <Î¨(5), Î¨â‚(5)>
}


/**
 * Computes the sign of the gamma function of a generalized dual number.
 *
 * If $(MATH f(x) = sgn(Î“(g(x)))), then $(MATH f' = 2ğ›¿(Î“(g))Î“(g)Î¨(g)g'). Since $(MATH Î“(g) â‰  0, âˆ€g),
 * $(MATH f' = 0), if it exists. It doesn't exists when $(MATH g) is a non-positive integer or
 * $(MATH -âˆ) or when $(MATH g') is infinite.
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN` argument
 *
 * Returns:
 *   It returns $(MATH  sgn(Î“(g))) as a `GDN`.
 */
pure nothrow @nogc @safe GDN!Deg sgnGamma(ulong Deg)(in GDN!Deg g)
{
    const f = std.mathspecial.sgnGamma(g.val);

    real df;

    if (!isNaN(f)) {
        if (g.val < 0) {
            ulong ngz = cast(ulong) trunc(-g.val);
            if (ngz != -g.val) df = (ngz & 1) == 0 ? -0. : +0.;
        } else if (g.val is -0.0L) {
            df = -0.;
        } else {
            df = +0.;
        }
    }

    return GDN!Deg(f, df * g.d);
}

///
unittest
{
    const f = sgnGamma(GDN!1(1));
    assert(f == 1 && f.d == 0);
}

unittest
{
// NB: This fails because of https://github.com/dlang/phobos/issues/10801, fixed in stable
//     const g = sgnGamma(GDN!1(-0.5));
//     assert(g == -1 && g.d == 0);

    const h = sgnGamma(GDN!1(+0.));
    assert(h is GDN!1(1, 0));

    const i = sgnGamma(GDN!1(-1));
    assert(isNaN(i.val) && isNaN(i.d));
}


/**
 * Computes the beta function where at least one of the arguments is a generalized dual number.
 *
 * If $(MATH f(x) = B(g(x),h(x))), then $(MATH f' = $(SUP âˆ‚B)/$(SUB âˆ‚g)g' + $(SUP âˆ‚B)/$(SUB âˆ‚h)h').
 * $(MATH $(SUP âˆ‚B(x$(SUB 1),x$(SUB 2)))/$(SUB âˆ‚x$(SUB i)) = Bâ‹…[Î¨(x$(SUB i)) - Î¨(x$(SUB 1)+x$(SUB 2))]),
 * so $(MATH f' = Bâ‹…[Î¨(g) - Î¨(g+h)]g' + Bâ‹…[Î¨(h) - Î¨(g+h)]h'). This reduces to
 * $(MATH f' = B(g,h)[Î¨(g)g' + Î¨(h)h' - Î¨(g+h)(g' + h')]).
 *
 * Params:
 *   G = the first `GDN` argument
 *   H = the second `GDN` argument
 *   g = the first `GDN` argument
 *   h = the second `GDN` argument
 *
 * Returns:
 *   $(MATH B(g,h)) as a `GDN`.
 */
pure nothrow @nogc @safe
CommonGDN!(G, H) beta(G, H)(in G g, in H h) if (isOne!(isGDN, G, H) && areAll!(isGDNOrReal, G, H))
{
    alias Deg = typeof(return).DEGREE;

    static if (Deg == 1) {
        alias B = std.mathspecial.beta;
        alias psi = std.mathspecial.digamma;
    } else {
        alias B = beta;
        alias psi = digamma;
    }

    const gg = asGDN!Deg(g);
    const hh = asGDN!Deg(h);

    const g_red = gg.reduce();
    const h_red = hh.reduce();
    const f_red = B(g_red, h_red);

    return GDN!Deg(
        asReal(f_red), f_red*(gg.d*psi(g_red) + hh.d*psi(h_red) - (gg.d + hh.d)*psi(g_red+h_red)));
}

///
unittest
{
    assert(beta(GDN!1(2), GDN!1(1)) == GDN!1(0.5, -1));
}

unittest
{
    // f' = B(g,h)[g'Î¨(g) + h'Î¨(h) - (g' + h')Î¨(g+h)]
    // In the following m and n are positive integers
    // B(m,n) = (m - 1)!(n - 1)!/(m + n - 1)!
    // Î¨(n) = -Î³ + âˆ‘áµ¢â‚Œâ‚â¿â»Â¹(1/i)
    // Î¨â‚(n) = ğœ‹Â²/6 - âˆ‘áµ¢â‚Œâ‚â¿â»Â¹(1/iÂ²)

    import std.format: format;
    import std.math: isClose;

    assert(beta(GDN!1(1), 2) is GDN!1(0.5, -0.75));

    const q_act = beta(3, GDN!1(4));
    const q_exp = GDN!1(1.0L/60, -37.0L/3_600);
    assert(q_act == q_exp);
    assert(isClose(q_act.d, q_exp.d), format("B'(3, <4,1>) = %s != %s", q_act.d, q_exp.d));
    // f = 2!3!/6! = 2/(6*5*4) = 1/(5*4*3) = 1/60
    // f' = 1/60[Î¨(4) - Î¨(7)]
    //    = [(1 + 1/2 + 1/3) - (1 + 1/2 + 1/3 + 1/4 + 1/5 + 1/6)]/60
    //    = -(1/4 + 1/5 + 1/6)/60
    //    = -(9/20 + 1/6)/60
    //    = -(27/60 + 10/60)/60
    //    = -37/60/60
    //    = -37/3600

    assert(beta(GDN!2(2), GDN!2(3)) == GDN!2(1.0L/12, -5.0L/36, 9.0L/32));
    // f = 1!2!/4! = 2/(4*3*2) = 1/12;
    // <f',f"> = B(<2,1>, <3,1>)[<1,0>Î¨(<2,1>) + <1,0>Î¨(<3,1>) - (<1,0> + <1,0>)Î¨(<2,1>+<3,1>)]
    // B(<2,1>, <3,1>) = <B(2,3), B(2,3)[Î¨(2) + Î¨(3) - (1 + 1)Î¨(2+3)]>
    //    = <B(2,3), B(2,3)[Î¨(2) + Î¨(3) - 2Î¨(5)]>
    // B(2,3) = 1/12
    // Î¨(<n,1>) = <Î¨(n),Î¨â‚(n)>
    // <1,0>Î¨(<2,1>) + <1,0>Î¨(<3,1>) - (<1,0> + <1,0>)Î¨(<2,1>+<3,1>)
    //    = <Î¨(2),Î¨â‚(2)> + <Î¨(3),Î¨â‚(3)> - <2,0><Î¨(5),Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3), Î¨â‚(2) + Î¨â‚(3)> - 2<Î¨(5),Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3), Î¨â‚(2) + Î¨â‚(3)> - <2Î¨(5),2Î¨â‚(5)>
    //    = <Î¨(2) + Î¨(3) - 2Î¨(5), Î¨â‚(2) + Î¨â‚(3) - 2Î¨â‚(5)>
    // Î¨(2) + Î¨(3) - 2Î¨(5) = -Î³ + 1 + -Î³ + 1 + 1/2 - 2(-Î³ + 1 + 1/2 + 1/3 + 1/4)
    //    = -2Î³ + 2 + 1/2 - 2Î³ - 2 - 1 - 2/3 - 1/2 = -1 - 2/3
    //    = -5/3
    // Î¨â‚(2) + Î¨â‚(3) - 2Î¨â‚(5) = ğœ‹Â²/6 - 1 + ğœ‹Â²/6 - (1 + 1/4) - 2[ğœ‹Â²/6 - (1 + 1/4 + 1/9 + 1/16)]
    //    = ğœ‹Â²/3 - 1 - 1 - 1/4 - 2(ğœ‹Â²/6 - 1 - 1/4 - 1/9 - 1/16)
    //    = ğœ‹Â²/3 - 2 - 1/4 - ğœ‹Â²/3 + 2 + 1/2 + 2/9 + 1/8 = 1/2 - 1/4 + 1/8 + 2/9 = 1/4 + 25/72
    //    = 43/72
    // <f',f"> = <1/12,(-5/3)/12><-5/3,43/72> = <1/12,-5/36><-5/3,43,72> = <-5/36,25/108 + 43/864>
    //    = <-5/36,9/32>

    const w_act = beta(GDN!1(-0.5), GDN!1(1));
    // f = B(-0.5, 1) = Î“(-0.5)Î“(1)/Î“(0.5) = ğœ‹/[sin(-ğœ‹/2)Î“(1.5)]/Î“(0.5) = -2ğœ‹Î“(1)/[âˆšğœ‹Î“(2)âˆšğœ‹]
    //   = -2
    // f' = B(-0.5,1)[Î¨(-0.5) + Î¨(1) - 2Î¨(0.5)]
    // Î¨(1.5) - Î¨(-0.5) = ğœ‹â‹…cot(-ğœ‹/2) =>
    // Î¨(-0.5) = Î¨(1.5) = -Î³ - 2ln(2) + âˆ‘áµ¢â‚Œâ‚Â¹2/(2i - 1)
    //         = 2 - Î³ - 2ln(2)
    // Î¨(0.5) = -Î³ - 2ln(2)
    // Î¨(1) = -Î³
    // f' = -2{2 - Î³ - 2ln(2) + -Î³ - 2[-Î³ - 2ln(2)]} = -2[2 - 2Î³ - 2ln(2) + 2Î³ + 4ln(2)]
    //    = -4 - 4ln(2)
    const w_exp = GDN!1(-2, -4-4*LN2);
    assert(
        isClose(w_act.val, w_exp.val) && isClose(w_act.d, w_exp.d),
        format("B(-.5,1) = %s != %s", w_act, w_exp));

    const e = beta(GDN!1(-1), GDN!1(2));
    assert(isNaN(e.val) && isNaN(e.d));
    // f = B(-1, 2) = Î“(-1)Î“(1)/Î“(1) = Î“(-1), DNE

    const r = beta(GDN!1(-0.5), GDN!1(0.5));
    assert(r.val is -0. && isNaN(r.d));
    // f = B(-.5, .5) = Î“(-.5)Î“(.5)/Î“(0) =
    //   = -0
    // f' = B(-.5, .5)[Î¨(-.5) + Î¨(.5) - 2Î¨(0)]

    const t = beta(GDN!1(-1), GDN!1(1));
    assert(isNaN(t.val) && isNaN(t.d), format("B(-1,1) != %s", t));
    // lim(x->1) B(-x,x) = lim(x->1) Î“(-x)Î“(x)/Î“(0) = 0
    // lim(x->1) B(-x,1) = lim(x->1) Î“(-x)Î“(1)/Î“(1 - x) = lim(x->1) Î“(-x)/Î“(1 - x)
    // lim(x->1) ğœ‹/[sin(-ğœ‹x)Î“(1+x)]/{ğœ‹/[sin(ğœ‹x)Î“(x)]} = lim(x->1) ğœ‹sin(ğœ‹x)Î“(x)/[ğœ‹sin(-ğœ‹x)Î“(1+x)]
    // lim(x->1) sin(ğœ‹x)Î“(x)/[-sin(ğœ‹x)Î“(1+x)] = lim(x->1) -Î“(x)/Î“(1+x) = -Î“(1)/Î“(2) = -1
    // B(-1,1) DNE

    assert(beta(GDN!1(2), GDN!1(-1.5)) is beta(GDN!1(-1.5), GDN!1(2)));

    const y = std.mathspecial.beta(-0.5, -0.2);
    const u = std.mathspecial.digamma(-0.5);
    const i = std.mathspecial.digamma(-0.2);
    const o = std.mathspecial.digamma(-0.7);
    assert(beta(GDN!1(-0.5), GDN!1(-0.2)) is GDN!1(y, y*(u + i - 2*o)));
    // f = B(-.5,-.2)
    // f' = B(-.5,-.2)[Î¨(-.5) + Î¨(-.2) - 2Î¨(-.7)]

// NB: Fails because std.mathspecial.beta(-1.5, -.5) is NaN. Fixed in stable branch.
    // const p = beta(GDN!1(-1.5), GDN!1(-0.5));
    // // f = B(-1.5,-.5) = Î“(-1.5)Î“(-.5)/Î“(-2) = -0
    // // f' = f[Î¨(-1.5) + Î¨(-.5) - 2Î¨(-2)] DNE
    // assert(p.val is -0.0L && isNaN(p.d), format("B(-1.5,-0.5) != %s", p));

    const a = beta(GDN!1(-1), GDN!1(-0.5));
    assert(isNaN(a.val) && isNaN(a.d));
    // f = B(-1, -0.5) = Î“(-1)Î“(-.5)/Î“(-1.5) DNE

    const s = beta(GDN!1(-1), GDN!1(-2));
    assert(isNaN(s.val) && isNaN(s.d));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const d = beta(GDN!1(-0.), GDN!1(-0.5));
//     // f' = B(-0, -0.5)[Î¨(-0) + Î¨(-0.5) - 2Î¨(-0.5)] = -âˆ[âˆ - Î¨(-0.5)] = -âˆ,
//     assert(d == -real.infinity && d.d == -real.infinity, format("B(-0,-.5) != %s", d));

    const f = beta(GDN!1(-0.), GDN!1(-1));
    assert(isNaN(f.val) && isNaN(f.d));

// NB: Fails because std.mathspecial.digamma(+0.) is NaN. Fixed in stable branch.
//     const g = beta(GDN!1(+0.), GDN!1(-1.5));
//     // f' = B(+0, -1.5)[Î¨(+0) + Î¨(-1.5) - 2Î¨(-1.5)] = âˆ[-âˆ - Î¨(-0.5)] = -âˆ,
//     assert(g == real.infinity && g.d == -real.infinity, format("B(+0, -1.5) = %s", g));

    const h = beta(GDN!1(+0.), GDN!1(-2));
    assert(isNaN(h.val) && isNaN(h.d));

// NB: Fails because std.mathspecial.digamma(+0.) = real.nan. Fixed in stable.
//    const j = beta(GDN!1(+0.), GDN!1(+0.));
//    assert(j == real.infinity && isNaN(j.d), format("B(+0,+0) = %s", j));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const k = beta(GDN!1(-0.), GDN!1(1));
//     // f' = B(-0, 1)[Î¨(-0) + Î¨(1) - 2Î¨(1)] = -âˆ[âˆ - Î¨(1)] = -âˆ,
//     assert(k == -real.infinity && k.d == -real.infinity, format("B(-0, 1) = %s", k));

// NB: Fails because std.mathspecial.digamma(-0.) is NaN. Fixed in stable branch.
//     const l = beta(GDN!1(+0.), GDN!1(1));
//     // f' = B(+0, 1)[Î¨(+0) + Î¨(1) - 2Î¨(1)] = âˆ[-âˆ - Î¨(1)] = -âˆ,
//     assert(l == real.infinity && l.d == -real.infinity, format("B(+0,1) = %s", l));

// NB: Fails because std.mathspecial.beta(real.infinity, 1) is NaN. Fixed in
// stable branch.
//     const z = beta(GDN!1(real.infinity), GDN!1(1));
//     assert(z == 0 && isNaN(z.d), format("B(âˆ,1) != %s", z));

// NB: Fails because std.mathspecial.beta(real.infinity, real.infinity) is NaN.
// Fixed in stable branch.
//     const x = beta(GDN!1(real.infinity), GDN!1(real.infinity));
//     assert(x == 0 && isNaN(x.d), format("B(âˆ,âˆ) = %s", x));

    const c = beta(GDN!1(-real.infinity), GDN!1(1));
    assert(isNaN(c.val) && isNaN(c.d));

    const v = beta(GDN!1.nan, GDN!1(1));
    assert(isNaN(v.val) && isNaN(v.d));
}


/**
 * the digamma function,$(MATH Î¨), of a generalized dual number
 *
 * If $(MATH f(x) = Î¨(g(x))), then $(MATH f' = Î¨â‚(g)g'), where $(MATH Î¨â‚) is the polygamma function
 * of order $(MATH 1) (trigamma function).
 *
 * Params:
 *   Deg = the degree of g
 *   g = the `GDN`argument
 *
 * Returns:
 *   $(MATH Î¨(g)) as a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg digamma(ulong Deg)(in GDN!Deg g)
{
    static if (Deg == 1)
        const df = ad.math.polygamma.polygamma!1(g.reduce());
    else
        const df = polygamma!1(g.reduce());

    return GDN!Deg(std.mathspecial.digamma(g.val), df*g.d);
}

///
unittest
{
    import ad.math.internal: isNaN;

    const trigamma_1 = PI^^2 / 6;  // Î¨â‚(1)

    const f_act = digamma(GDN!1(1));
    const f_exp = GDN!1(std.mathspecial.digamma(1), trigamma_1);
    assert(f_act == f_exp);

    assert(isNaN(digamma(GDN!1(-1))));
    assert(digamma(GDN!1(real.infinity)) is GDN!1(real.infinity, +0.));
}

unittest
{
    import std.format: format;

    const e = GDN!1(std.mathspecial.digamma(2), 3*ad.math.polygamma.polygamma!1(2));
    assert(digamma(GDN!1(2, 3)) is e);

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const f_nz = digamma(GDN!1(-0.));
//     assert(f_nz is GDN!1(real.infinity, real.infinity), format("Î¨â‚(-0) = %s", f_nz));

// NB: Fails because of https://github.com/dlang/phobos/issues/10802, fixed on stable
//     const f_pz = digamma(GDN!1(+0.));
//     assert(f_pz is GDN!1(-real.infinity, real.infinity), format("Î¨â‚(+0) = %s", f_pz));

    const q = digamma(GDN!1(-real.infinity));
    assert(isNaN(q.val) && isNaN(q.d));

    const g = digamma(GDN!1(real.nan));
    assert(isNaN(g.val) && isNaN(g.d));

    const w = GDN!2(
        std.mathspecial.digamma(2),
        ad.math.polygamma.polygamma!1(2),
        ad.math.polygamma.polygamma!2(2));

    assert(digamma(GDN!2(2)) is w);
}


/**
 * The natural logarithm of a `GDN` minus digamma of the same `GDN`.
 *
 * If $(MATH f(x) = ln(g(x)) - Î¨(g(x))), then $(MATH f' = [1/g - Î¨â‚(g)]g'), where $(MATH Î¨â‚) is the
 * polygamma function of order one (trigamma function).
 *
 * Params:
 *   Deg = the degree of g
 *   g = the argument, must be positive
 *
 * Returns:
 *   a GDN representing the natural logarithm of g minus digamma of g.
 */
pure nothrow @nogc @safe GDN!Deg logmdigamma(ulong Deg)(in GDN!Deg g)
in(signbit(g) == 0, "the argument must be positive")
do {
    const f = std.mathspecial.logmdigamma(g.val);
    const g_red = g.reduce();

    static if (Deg == 1)
        const dfdg = 1/g_red - ad.math.polygamma.polygamma!1(g_red);
    else
        const dfdg = g_red.inv() - polygamma!1(g_red);

    return GDN!Deg(f, dfdg*g.d);
}

///
unittest
{
    import std.math: isClose;

    // Euler-Mascheroni constant
    const Î³ = 0.577_215_664_901_532_860_607L;

    const f_act = logmdigamma(GDN!1(1));
    const f_exp = GDN!1(Î³, 1-PI^^2/6);
    assert(isClose(f_act.val, f_exp.val) && isClose(f_act.d, f_exp.d));
}

unittest
{
    import std.format: format;
    import std.math: isClose;
    import ad.math: log;

    const w = logmdigamma(GDN!1(+0.));
    assert(w == real.infinity && isNaN(w.d), format("logmdigamma(+0) != %s", w));
    // ln(x) - 1/x â‰¤ Î¨(x) â‰¤ ln(x) - 1/(2x), x>0
    // -1/x â‰¤ Î¨(x) - ln(x) â‰¤ -1/(2x)
    // 1/x â‰¥ ln(x) - Î¨(x) â‰¥ 1/(2x)
    // lim{xâŸ¶0âº} ln(x) - Î¨(x) â‰¥ lim{xâŸ¶0âº} 1/(2x) = +âˆ

    const e_act = logmdigamma(GDN!2(2));
    const e_exp = log(GDN!2(2)) - digamma(GDN!2(2));
    assert(isClose(e_act.val, e_exp.val));
    assert(isClose(e_act.d.val, e_exp.d.val, 10*real.epsilon));
    assert(e_act.d!2 == e_exp.d!2);
}


/**
 * The inverse of the function formed from the natural logarithm of a `GDN` minus digamma of the
 * same `GDN`.
 *
 * If $(MATH f(x) = ln(g(x)) - Î¨(g(x))), then $(MATH g = fâ»Â¹(f)) and $(MATH f' = [1/g - Î¨â‚(g)]g'),
 * where $(MATH Î¨â‚) is the polygamma function of order one (trigamma function). Thus
 * $(MATH g' = f' / [1/g - Î¨â‚(g)] = f'g / [1 - gÎ¨â‚(g)])
 *
 * Params:
 *   Deg = the degree of f
 *   f = the `GDN` argument
 *
 * Return:
 *   It returns `g` such that the natural logarithm of `g` minus digamma of `g` is equal to f.
 */
pure nothrow @nogc @safe GDN!Deg logmdigammaInverse(ulong Deg)(in GDN!Deg f)
{
    static if (Deg == 1) {
        alias ln_m_digamma_inv = std.mathspecial.logmdigammaInverse;
        alias trigamma = ad.math.polygamma.polygamma!1;
    } else {
        alias ln_m_digamma_inv =  logmdigammaInverse;
        alias trigamma = polygamma!(1, Deg-1);
    }

    const g_red = ln_m_digamma_inv(f.reduce());

    GDN!Deg.DerivType!1 naive_derivative() { return f.d * g_red / (1 - g_red*trigamma(g_red)); }

    // Assume x > 0. (x + 1/2)/xÂ² â‰¤ Î¨â‚(x) â‰¤ (x + 1)/xÂ² â‡’ -1/x â‰¤ 1 - xÎ¨â‚(x) â‰¤ -1/(2x).
    // lim{xâŸ¶0âº} -1/x = -âˆ = lim{xâŸ¶0âº} -1/(2x) â‡’ lim{xâŸ¶0âº} 1 - xÎ¨â‚(x) = -âˆ
    // lim{xâŸ¶+âˆ} -1/x = 0â» = lim{xâŸ¶+âˆ} -1/(2x) â‡’ lim{xâŸ¶+âˆ} 1 - xÎ¨â‚(x) = 0â»
    GDN!Deg.DerivType!1 dg;

    if (g_red == 0 && signbit(g_red) == 0) {
        static if (Deg == 1)
            dg = -0. * f.d;
        else
            dg = GDN!Deg.DerivType!1(asReal(g_red.d), naive_derivative().d);
    } else if (g_red == real.infinity) {
        static if (Deg == 1)
            dg = -real.infinity * f.d;
        else
            dg = GDN!Deg.DerivType!1(asReal(g_red.d), naive_derivative().d);
    } else {
        dg = naive_derivative();
    }

    return GDN!Deg(asReal(g_red), dg);
}

///
unittest
{
    import std.math: isClose;

    const f = GDN!1(1);
    const g = logmdigammaInverse(logmdigamma(f));
    assert(isClose(g.val, f.val) && isClose(g.d, f.d));
}

unittest
{
    import std.format: format;
    import std.math: isClose;

    const Î³ = 0.577_215_664_901_532_860_607L;
    const Î¶3 = 1.202_056_903_159_594_285_400L;

    const q = logmdigammaInverse(GDN!1(+0.0L));
    // g = +âˆ
    // g' = lim{gâŸ¶+âˆ} 1/[1/g - Î¨â‚(g)] = lim{gâŸ¶0âº} 1/g - Î¨â‚(g) = -lim{gâŸ¶0âº} Î¨â‚(g) - 1/g
    //    â‰¤ -lim{gâŸ¶0âº} (g + 1)/gÂ² - 1/g = -lim{gâŸ¶0âº} 1/gÂ² = -âˆ
    // g' = -âˆ
    assert(q is GDN!1(real.infinity, -real.infinity), format("logmdigammaInverse(+0) != %s", q));

    const w = logmdigammaInverse(GDN!1(real.infinity));
    // lim{gâŸ¶0âº} Î¨(g) ~ lim{gâŸ¶0âº} ln(g) - 1/(2g) â‡’ lim{gâŸ¶0âº} Î¨(g) - ln(q) ~ lim{gâŸ¶0âº} -1/(2g)
    // â‡’ lim{gâŸ¶0âº} ln(g) - Î¨(g) ~ lim{gâŸ¶0âº} 1/(2g) = +âˆ
    // â‡’ g = 0âº
    // g' = lim{gâŸ¶0âº} 1/[1/g - Î¨â‚(g)] = lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) = -lim{gâŸ¶+âˆ} Î¨â‚(g) - 1/g
    // -lim{gâŸ¶+âˆ} (g + 1/2)/gÂ² - 1/g â‰¤ -lim{gâŸ¶+âˆ} Î¨â‚(g) - 1/g â‰¤ -lim{gâŸ¶+âˆ} (g + 1)/gÂ² - 1/g
    // â‡’ lim{gâŸ¶+âˆ} 1/g - (g + 1/2)/gÂ² â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ lim{gâŸ¶+âˆ} 1/g - (g + 1)/gÂ²
    // â‡’ lim{gâŸ¶+âˆ} -1/(2gÂ²) â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ lim{gâŸ¶+âˆ} -1/gÂ²
    // â‡’ 0â» â‰¤ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) â‰¤ 0â» â‡’ lim{gâŸ¶+âˆ} 1/g - Î¨â‚(g) = 0â»
    // g' = 0â»
    assert(w is GDN!1(+0., -0.), format("logmdigammaInverse(âˆ) != %s", w));

// NB: Failed because std.mathspecial.logmdigammaInverse(-.0) is -real.infinity, fixed in stable
//     const e = logmdigammaInverse(GDN!1(-0.));
//     // Assume x > 0. ln(x) - 1/(2x) - Î¨â‚(x) > 0 â‡’ ln(x) - Î¨â‚(x) > 1/(2x) > 0.
//     // f(x) = ln(x) - Î¨â‚(x) is defined for x âˆŠ â„âº. f: â„âº â†¦ â„âº â‡’ fâ»Â¹: â„âº â†¦ â„âº
//     // f(-0) DNE,
//     assert(isNaN(e), format("logmdigammaInverse(-0) = %s", e));

    const r = logmdigammaInverse(GDN!1(-1));
    assert(isNaN(r.val) && isNaN(r.d));

    const t = GDN!2(Î³);
    const u = logmdigammaInverse(t);
    // ln(1) - Î¨(1) == Î³ â‡’ g = 1
    // g' = f'g/[1 - gÎ¨â‚(g)] = 1*1/[1 - 1*Î¨â‚(1)] = 1/(1 - ğœ‹Â²/6) = 6/(6 - ğœ‹Â²)
    // <g',g"> = <f',f"><g,g'>/[1 - <g,g'>Î¨â‚(<g,g'>)]
    //         = <1,0><1,g'>/[1 - <1,g'>Î¨â‚(<1,g'>)]
    //         = <1,g'>/[1 - <1,g'><Î¨â‚(1),Î¨â‚‚(1)g'>]
    // Î¨â‚‚(1) = (-1)Â³2!Î¶(3,1) = -2Î¶(3)
    // <g',g"> = <1,g'>/[1 - <1,g'><ğœ‹Â²/6,-2Î¶(3)g'>]
    //         = <1,g'>/[1 - <ğœ‹Â²/6,ğœ‹Â²g'/6 - 2Î¶(3)g'>]
    //         = <1,g'>/<1 - ğœ‹Â²/6,[2Î¶(3) - ğœ‹Â²/6]g'>
    //         = <1/(1 - ğœ‹Â²/6),{(1 - ğœ‹Â²/6)g' - [2Î¶(3) - ğœ‹Â²/6]g'}/(1 - ğœ‹Â²/6)Â²>
    //         = <g',[1 - 2Î¶(3)](g')Â³>
    // g" = [1 - 2Î¶(3)](g')Â³ = [1 - 2Î¶(3)][6/(6 - ğœ‹Â²)]Â³
    // g" = 216[1 - 2Î¶(3)]/(6 - ğœ‹Â²)Â³
    const dg = 6/(6 - PI^^2);
    const d2g = 216*(1 - 2*Î¶3)/(6 - PI^^2)^^3;
    assert(
        isClose(u.val, 1) && isClose(u.d.val, dg) && isClose(u.d!2, d2g),
        format("logmdigammaInverse(Î³) != %s", u));
}


/* This function computes Pâ‚“(s,x), the partial derivative of the regularized
 * lower incomplete gamma function with respect to x.
 *
 * In summary:
 *
 *    - Pâ‚“(0âº,x) = ğ›¿(x)
 *    - Pâ‚“(s,0) = { âˆ, 0<s<1; 1, s=1; 0, 1<s<âˆ }
 *    - Pâ‚“(s,x) = xË¢â»Â¹eâ»Ë£/ğ›¤(s), 0 < s < âˆ, x > 0
 *    - Pâ‚“(âˆ,x) = { 0, 0â‰¤x<âˆ; âˆ, x=âˆ }
 *
 * In detail:
 *
 * P(s,x) = ğ›¾(s,x)/ğ›¤(s), where ğ›¾(s,x) is the lower incomplete gamma function.
 * Pâ‚“(s,x) = ğ›¾â‚“(s,x)/ğ›¤(s).
 *
 * ğ›¾(s,x) = âˆ«â‚€Ë£tË¢â»Â¹eâ»áµ—dt. The integrand tË¢â»Â¹eâ»áµ— is Lebesgue integrable over
 * 0 â‰¤ t â‰¤ âˆ. Therefore, ğ›¾â‚“(s,x) = xË¢â»Â¹eâ»Ë£ almost everywhere. x = 0 is the only
 * value where xË¢â»Â¹eâ»Ë£ doesn't exist for every positive s, but the one-sided
 * limit from above does. This algorithm defines
 * ğ›¾â‚“(s,0) = lim{xâ†’0âº} xË¢â»Â¹eâ»Ë£ = { âˆ, 0<s<1; 1, s=1; 0, s>1 }.
 *
 * Thus when 0 < s < âˆ, Pâ‚“(s,x) = xË¢â»Â¹eâ»Ë£/ğ›¤(s), if x > 0, and
 * Pâ‚“(s,0) = { âˆ, 0<s<1; 1, s=1; 0, s>1 }, if x = 0.
 *
 * Define P(0,x) = lim{sâ†’0âº} P(s,x). P : (0,âˆ)â¨¯[0,âˆ] â†’ [0,1] and is
 * non-decreasing, since it is a special case of the gamma CDF. This means
 * P(s,0) = 0 for all s, i.e., P(0,0) = 0. Now assume x > 0.
 * P(0,x) = lim{sâ†’0âº} ğ›¾(s,x)/ğ›¤(s). Thus
 * P(0,x) = lim{sâ†’0âº} âˆ«â‚€â±tË¢â»Â¹eâ»áµ—dt/ğ›¤(s) + lim{sâ†’0âº} âˆ«áµ¢Ë£tË¢â»Â¹eâ»áµ—dt/ğ›¤(s), where
 * 0 < i < x. Choose i to be small enough that eâ»áµ— â‰ˆ 1 when 0 â‰¤ t â‰¤ i.
 * âˆ«â‚€â±tË¢â»Â¹eâ»áµ—dt â‰ˆ âˆ«â‚€â±tË¢â»Â¹dt = [tË¢/s]â‚€â± = iË¢/s. As sâ†’0âº, ğ›¤(s) ~ 1/s, so
 * lim{sâ†’0âº} âˆ«â‚€â±tË¢â»Â¹eâ»áµ—dt/ğ›¤(s) = lim{sâ†’0âº} (iË¢/s)/(1/s) = lim{sâ†’0âº} iË¢ = 1.
 *
 * lim{sâ†’0âº} âˆ«áµ¢Ë£tË¢â»Â¹eâ»áµ—dt = âˆ«áµ¢Ë£(lim{sâ†’0âº} tË¢â»Â¹eâ»áµ—)dt = âˆ«áµ¢Ë£(eâ»áµ—/t)dt.
 * 0 < âˆ«áµ¢Ë£(eâ»áµ—/t)dt < âˆ«áµ¢Ë£eâ»áµ—dt/i = [-eâ»áµ—]áµ¢Ë£/i = (eâ»â± - eâ»Ë£)/i < 1/i.
 * lim{sâ†’0âº} âˆ«áµ¢Ë£tË¢â»Â¹eâ»áµ—dt/ğ›¤(s) â‰¤ lim{sâ†’0âº} 1/(iğ›¤(s)) = 0.
 *
 * Thus P(0âº,x) = { 0, x=0; 1, x>0 }, and Pâ‚“(0âº,x) = ğ›¿(x).
 *
 * Define P(âˆ,x) = lim{sâ†’âˆ} P(s,x). P(s,âˆ) = 1 for all s, since it is a special
 * case of the gamma CDF. I.e., P(âˆ,âˆ) = 1. Now assume x < âˆ.
 * P(âˆ,x) = lim{sâ†’âˆ} ğ›¾(s,x)/ğ›¤(s). ğ›¾(s,x) = ğ›¤(s)xË¢eâ»Ë£ğ›´â‚–â‚Œâ‚€xáµ/ğ›¤(s+k+1). The series
 * converges uniformly for all s and x, so
 * P(âˆ,x) = eâ»Ë£ğ›´â‚–â‚Œâ‚€lim{sâ†’âˆ}xË¢âºáµ/ğ›¤(s+k+1).
 * lim{sâ†’âˆ} xË¢âºáµ/ğ›¤(s+k+1) = lim{sâ†’âˆ} [ex/(s+k)]Ë¢âºáµ/âˆš[2ğœ‹(s+k)]. There exists sâ‚–
 * such that when s > sâ‚–, ex/(s+k) < 1. Thus lim{sâ†’âˆ} [ex/(s+k)]Ë¢âºáµ = 0. Since
 * lim{sâ†’âˆ} âˆš[2ğœ‹(s+k)] = âˆ, lim{sâ†’âˆ} xË¢âºáµ/ğ›¤(s+k+1) = 0, and
 * P(âˆ,x) = eâ»Ë£ğ›´â‚–â‚Œâ‚€0 = 0 when x < âˆ. This means that
 * P(âˆ,x) = { 0, 0â‰¤x<âˆ; 1, x=âˆ }, and Pâ‚“(âˆ,x) = { 0, 0â‰¤x<âˆ; âˆ, x=âˆ }.
 */
private pure nothrow @nogc @safe
GDN!Deg.DerivType!1 gammaIncompleteDeriv(ulong Deg)(in real s, in GDN!Deg x)
do {
    alias dType = typeof(return);

    if (signbit(s) == 1 || x < .0L) return dType.nan;

    const x_red = x.reduce();

    if (s == .0L) {
        return dirac(x_red);
    } else if (s is real.infinity) {
        return x.val is real.infinity ? dirac(GDN!Deg(-0.0L, x.d)).reduce() : GDN!Deg.zero.reduce();
    } else {
        // Ensure that x = -0 is treated like x = +0
        static if (Deg == 1) {
            alias e = std.math.exp;
            const x_red_pos = x_red is -0.0L ? +0.0L : x_red;
        } else {
            alias e = exp;
            const x_red_pos = dType(x_red.val is -0.0L ? +0.0L : x_red.val, x_red.d);
        }

        if (x.val is real.infinity) {
            return GDN!Deg.one.d;
        } else {
            return x_red_pos^^(s - 1.0L) / (e(x_red_pos) * std.mathspecial.gamma(s));
        }
    }
}
unittest {
    import std.format: format;

    const a_act = gammaIncompleteDeriv(.5L, GDN!1(.25L));
    const a_exp = 2.0L / (E^^.25L * sqrt(PI));
    assert(isClose(a_act, a_exp), format("Pâ‚“(.5, .25) = %s â‰  %s", a_act, a_exp));

    const b_act = gammaIncompleteDeriv(.5L, GDN!1(1.0L));
    const b_exp = 1.0L / (E * sqrt(PI));
    assert(isClose(b_act, b_exp), format("Pâ‚“(.5, 1) = %s â‰  %s", b_act, b_exp));

    const c_act = gammaIncompleteDeriv(.5L, GDN!1(9.0L));
    const c_exp = 1.0L / (3.0L * E^^9.0L * sqrt(PI));
    assert(isClose(c_act, c_exp), format("Pâ‚“(.5, 9) = %s â‰  %s", c_act, c_exp));

    assert(gammaIncompleteDeriv(.1L, GDN!1(real.infinity)) == .0L);
    assert(gammaIncompleteDeriv(1.0L, GDN!1(.1L)) == E ^^ -.1L);

    const d_act = gammaIncompleteDeriv(1.0L, GDN!1(10.0L));
    const d_exp = E ^^ -10.0L;
    assert(isClose(d_act, d_exp), format("Pâ‚“(1, 10) = %s â‰  %s", d_act, d_exp));

    assert(gammaIncompleteDeriv(1.0L, GDN!1(real.infinity)) == .0L);

    assert(gammaIncompleteDeriv(2.0L, GDN!1(.1L)) == 1.0L / (10.0L * E^^.1L));
    // f' = .1exp(-.1)/ğ›¤(2) = 1/(10*exp(.1))

    assert(gammaIncompleteDeriv(3.0L ,GDN!1(1.0L)) == 1.0L / (2.0L * E));

    assert(gammaIncompleteDeriv(4.0L, GDN!1(10.0L)) == 500.0L / (3.0L * E^^10.0L));
    // f' = 10Â³eâ»Â¹â°/ğ›¤(4)) = 500/(3eÂ¹â°)

    assert(gammaIncompleteDeriv(10.0L, GDN!1(real.infinity)) == .0L);
    assert(gammaIncompleteDeriv(.1L, GDN!1(.0L)) is real.infinity);
    assert(gammaIncompleteDeriv(1.0L, GDN!1(.0L)) == 1.0L);
    assert(gammaIncompleteDeriv(10.0L, GDN!1(.0L)) == .0L);
    assert(gammaIncompleteDeriv(.0L, GDN!1(.0L)) is real.infinity);
    assert(gammaIncompleteDeriv(.0L, GDN!1(1.0L)) == .0L);
    assert(gammaIncompleteDeriv(real.infinity, GDN!1(1.0L)) == .0L);
    assert(gammaIncompleteDeriv(real.infinity, GDN!1(real.infinity)) is real.infinity);
    assert(gammaIncompleteDeriv(1.0L, GDN!1(2.0L, 2.0L)) == E ^^ -2.0L);

    const e = gammaIncompleteDeriv(.5L, GDN!2(.0L));
    // <f',f"> = <0,1>^-.5â‹…exp(-<0,1>)/ğ›¤(.5) = <âˆ,-.5(0^-1.5))>exp(<0,-1>)/âˆšğœ‹ = <âˆ,-âˆ><1,-1>/âˆšğœ‹
    //    = <âˆ,-âˆ-âˆ>/âˆšğœ‹ = <âˆ,-âˆ>
    assert(e.val is real.infinity && e.d is -real.infinity, format("Pâ‚“(.5, 0) = %s", e));

    assert(gammaIncompleteDeriv(1.0L, GDN!2(.0L)) is GDN!1(1.0L, -1.0L));
    // <f',f"> = <1,0><0,1>^0â‹…exp(-<0,1>)/ğ›¤(1) = <1,0><1,0>exp(<0,-1>)/1 = <1,0><1,-1> = <1,-1>

    assert(gammaIncompleteDeriv(2.0L, GDN!2(.0L)) is GDN!1(.0L, 1.0L));
    // <f',f"> = <1,0><0,1>^1â‹…exp(-<0,1>)/ğ›¤(2) = <1,0><0,1>exp(<0,-1>)/1 = <0,1><1,-1> = <0,1>

    assert(gammaIncompleteDeriv(1.0L, GDN!2(1.0L)) is GDN!1(1.0L/E, -1.0L/E));
    // <f',f"> = <1,0><1,1>^0â‹…exp(-<1,1>)/ğ›¤(1) = exp(<-1,-1>)/1 = <1/e,-1/e>

    const f = gammaIncompleteDeriv(.0L, GDN!2(.0L));
    // <f',f"> = <1,0>ğ›¿(<0,1>) = <ğ›¿(0),-ğ›¿(0)> = <âˆ,-âˆ>
    assert(f.val is real.infinity && f.d is -real.infinity);

    assert(gammaIncompleteDeriv(.0L, GDN!2(1.0L)) is GDN!1(.0L, .0L));
    assert(gammaIncompleteDeriv(real.infinity, GDN!2(1.0L)) is GDN!1(.0L, .0L));

    const g = gammaIncompleteDeriv(real.infinity, GDN!2(real.infinity));
    // Define ğ›¿â‚— : [-âˆ,c] â†¦ [-âˆ,âˆ], where c âˆŠ â„ as ğ›¿â‚—(x; c) = { 0, x<c; âˆ, x=c } with
    // âˆ«ğ›¿â‚—(x; c)dx = 1. Notice that ğ›¿â‚—(x; c) = ğ›¿(x-c) when x â‰¤ c.
    // ğ›¿'(x) = { âˆ, x=0â»; -âˆ, x=0âº; 0, xâ‰ 0 }. Thus ğ›¿â‚—'(x; c) = { 0, x<c; âˆ, x=câ» }.
    // Define ğ›¿â‚—(x; âˆ) = lim{câ†’âˆ} ğ›¿â‚—(x; c) = { 0, x<âˆ; âˆ, x=âˆ }.
    // ğ›¿â‚—'(x; âˆ) = lim{câ†’âˆ} ğ›¿â‚—'(x; c) =  { 0, x<âˆ; âˆ, x=âˆ }.
    //
    // <f',f"> = Pâ‚“(âˆ,<âˆ,1>) = ğ›¿â‚—(<âˆ,1>; âˆ) = <ğ›¿â‚—(âˆ; âˆ),1ğ›¿â‚—'(âˆ; âˆ)> = <âˆ,âˆ>
    assert(g.val is real.infinity && g.d is real.infinity, format("Pâ‚“(âˆ,âˆ) = %s", g));

    assert(gammaIncompleteDeriv(1.0L, GDN!2(1.0L, 1.0L, 1.0L)) is GDN!1(1.0L/E, -1.0L/E));
    // <f',f"> = <1,1>^0â‹…exp(-<1,1>)/ğ›¤(1) = exp(<-1,-1>) = <1/e,-1/e>

    assert(gammaIncompleteDeriv(1.0L, GDN!2(1.0L, 2.0L, .0L)) is GDN!1(1.0L/E, -2.0L/E));
    // <f',f"> = <1,2>^0â‹…exp(-<1,2>)/ğ›¤(1) = exp(<-1,-2>) = <1/e,-2/e>

    assert(gammaIncompleteDeriv(1.0L, GDN!2(1.0L, .0L, 1.0L)) is GDN!1(1.0L/E, .0L));
    // <f',f"> = <1,0>^0â‹…exp(-<1,0>)/ğ›¤(1) = exp(<-1,0>) = <1/e,0>
}


/** The regularized lower incomplete gamma function $(MATH P(a,g)).
 *
 * $(MATH P(a,g) = ğ›¾(a,g)/ğ›¤(a)), where $(MATH ğ›¾(a,g) = âˆ«$(SUB 0)$(SUP g)t$(SUP a-1)e$(SUP -t)dt) is
 * the lower incomplete gamma function.
 *
 * Let $(MATH f(x) = P(a,g(x))). Then
 * $(MATH f' = $(SUP âˆ‚P)/$(SUB âˆ‚g)g' = g'g$(SUP a-1)e$(SUP -g)/ğ›¤(a)).
 *
 * Params:
 *   Deg = the degree of g
 *   a = the shape parameter, must be positive
 *   g = the argument, must be $(MATH â‰¥ 0).
 *
 * Returns:
 *   _a GDN representing $(MATH P(a,g)).
 */
pure nothrow @nogc @safe GDN!Deg gammaIncomplete(ulong Deg)(in real a, in GDN!Deg g)
in {
    if (!any!(std.math.isNaN)(only(a, g.val))) {
        assert(signbit(a) == 0, "the shape parameter must be positive");
        assert(g >= 0, "the argument must greater than or equal to 0");
    }
}
out(res; isNaN!Deg(res) || (res >= 0 && res <= 1), "result should be in [0,1]")
do {
    if (any!(std.math.isNaN)(only(a, g.val))) return GDN!Deg.nanCombine(g, asGDN!Deg(a));
    return GDN!Deg(std.mathspecial.gammaIncomplete(a, g.val), g.d*gammaIncompleteDeriv(a, g));
}
///
unittest {
    import std.math: E;

    assert(gammaIncomplete(1, GDN!1(1, 2)) is GDN!1(1-1/E, 2/E));
}
unittest {
    import std.format: format;

    const a = gammaIncomplete(NaN(0x1UL), GDN!1(.0L, NaN(0x2UL)));
    assert(isNaN(a) && getNaNPayload(a) == 0x1UL && getNaNPayload(a.d) == 0x2UL);

    const b_g = GDN!1(2.0L, 2.0L);
    const b_act = gammaIncomplete(1.0L, b_g);
    const b_exp = GDN!1(1.0L-1.0L/E^^2, 2.0L/E^^2);
    assert(b_act is b_exp, format("P(1, %s) = %s â‰  %s", b_g, b_act, b_exp));

    const c = gammaIncomplete(.5L, GDN!2(-0.0L));
    // <f',f"> = <1,0><0,1>^-.5â‹…exp(-<0,1>)/ğ›¤(.5) = <1,0><âˆ,-.5>exp(<0,-1>)/âˆšğœ‹ = <âˆ,NaN><1,-1>/âˆšğœ‹
    //    = <âˆ,NaN>/âˆšğœ‹ = <âˆ,NaN>
    assert(c == 0.0L && c.d.val is real.infinity && isNaN(c.d!2));

    assert(gammaIncomplete(1.0L, GDN!2(1.0L, 1.0L, 1.0L)) is GDN!2(1.0L-1.0L/E, 1.0L/E, .0L));
    // <f',f"> = <1,1><1,1>^0â‹…exp(-<1,1>)/ğ›¤(1) = <1,1>exp(<-1,-1>) = <1,1><1/e,-1/e>
    //    = <1/e,1/e + -1/e> = <1/e,0>

    assert(gammaIncomplete(1.0L, GDN!2(1.0L, 2.0L, .0L)) is GDN!2(1.0L-1.0L/E, 2.0L/E, -4.0L/E));
    // <f',f"> = <2,0><1,2>^0â‹…exp(-<1,2>)/ğ›¤(1) = <2,0>exp(<-1,-2>) = <2,0><1/E,-2/E> = <2/E,-4/E>

    assert(gammaIncomplete(1.0L, GDN!2(1.0L, .0L, 1.0L)) is GDN!2(1.0L-1.0L/E, .0L, 1.0L/E));
    // <f',f"> = <0,1><1,0>^0â‹…exp(-<1,0>)/ğ›¤(1) = <0,1>exp(<-1,0>) = <0,1><1/E,0> = <0,1/E>
}


/** The regularized upper incomplete gamma function $(MATH Q(a,g)).
 *
 * $(MATH Q(a,g) = ğ›¤(a,g)/ğ›¤(a)), where $(MATH ğ›¤(a,g) = âˆ«$(SUB g)$(SUP âˆ)t$(SUP a-1)e$(SUP -t)dt) is
 * the upper incomplete gamma function. Notice that $(MATH Q(a,g) = 1 - P(a,g)).
 *
 * Let $(MATH f(x) = Q(a,g(x))).
 * Then $(MATH f' = $(SUP âˆ‚Q)/$(SUB âˆ‚g)g' = -g'g$(SUP a-1)e$(SUP -g)/ğ›¤(a)).
 *
 * Params:
 *   Deg = the degree of g
 *   a = the shape parameter, must be positive
 *   g = the argument, must be $(MATH â‰¥ 0).
 *
 * Returns:
 *   _a GDN representing $(MATH Q(a,g)).
 */
pure nothrow @nogc @safe GDN!Deg gammaIncompleteCompl(ulong Deg)(in real a, in GDN!Deg g)
in {
    if (!any!(std.math.isNaN)(only(a, g.val))) {
        assert(signbit(a) == 0, "the shape parameter must be positive");
        assert(g >= 0, "the argument must greater than or equal to 0");
    }
}
out(res; isNaN!Deg(res) || (res >= 0 && res <= 1), "result should be in [0,1]")
do {
    if (any!(std.math.isNaN)(only(a, g.val))) return GDN!Deg.nanCombine(g, asGDN!Deg(a));
    return GDN!Deg(std.mathspecial.gammaIncompleteCompl(a, g.val), -g.d*gammaIncompleteDeriv(a, g));
}
///
unittest {
    import std.math: E, isClose;

    assert(gammaIncompleteCompl(1, GDN!1(1, 2)) is GDN!1(1/E, -2/E));

    const s = 2, x = GDN!1(3);
    const p = gammaIncomplete(s, x);
    const q = gammaIncompleteCompl(s, x);
    assert(isClose(q, 1-p) && q.d == (1-p).d);
}
unittest {
    import std.format: format;

    const a = gammaIncompleteCompl(NaN(0x1UL), GDN!1(.0L, NaN(0x2UL)));
    assert(isNaN(a) && getNaNPayload(a) == 0x1UL && getNaNPayload(a.d) == 0x2UL);

    const b = gammaIncompleteCompl(1.0L, GDN!1(1.0L, .0L));
    assert(b == 1.0L/E && b.d == .0L);

    assert(gammaIncompleteCompl(1.0L, GDN!2(.0L)) is GDN!2(1.0L, -1.0L, 1.0L));
    // <f',f"> = -<1,0><0,1>^(1-1)exp(-<0,1>)/ğ›¤(1) = <-1,0><0,1>^0â‹…exp(<0,-1>)/1
    //    = <-1,0><1,0><exp(0),-exp(0)> = <-1,0><1,-1>
    //    = <-1,1>

    assert(gammaIncompleteCompl(1.0L, GDN!2(1.0L, 1.0L, 1.0L)) is GDN!2(1.0L/E, -1.0L/E, .0L));
    // <f',f"> = -<1,1><1,1>^(1-1)exp(-<1,1>)/ğ›¤(1) = <-1,-1><1,1>^0â‹…exp(<-1,-1>)/1
    //    = <-1,-1><1,0><1/e,-1/e> = <-1,-1><1/e,-1/e> = <-1/e,-1/e+1/e>
    //    = <-1/e,0>

    const c_g = GDN!2(1.0L, -1.0L, .0L);
    const c = gammaIncompleteCompl(2.0L, c_g);
    // <f',f"> = -<-1,0><1,-1>^(2-1)exp(-<1,-1>)/ğ›¤(2) = <1,0><1,-1>^1â‹…exp(<-1,1>)
    //    = <1,0><1,-1><1/e,1/e> = <1,-1><1/e,1/e> = <1/e,-1/e+1/e>
    //    = <1/e,0>
    assert(isClose(c, 2.0L/E), format("Q(2, %s) = %s â‰  %s", c_g, c.val, 2.0L/E));
    assert(c.d is GDN!1(1.0L/E, .0L));

    const d = gammaIncompleteCompl(2.0L, GDN!2(2.0L, .0L, -1.0L));
    // f = (2-1)!exp(-2)[2^0/0! + 2^1/1!] = 1!exp(-2)[1 + 2] = 3exp(-2)
    // <f',f"> = -<0,-1><2,0>^(2-1)exp(-<2,0>)/ğ›¤(2) = <0,1><2,0>exp(<-2,0>) = <0,2><exp(-2),0>
    //    = <0,2/exp(2)>
    assert(d == 3.0L/E^^2);
    assert(d.d == .0L);
    assert(d.d!2 == 2.0L/E^^2);
}


/** The inverse regularized upper incomplete gamma function $(MATH Q$(SUP -1)(a,q)), fixed $(MATH a)
 *
 * If $(MATH q(x) = Q(a,g(x))), then $(MATH g = q$(SUP -1)(q)). $(MATH q' = $(SUP âˆ‚Q)/$(SUB âˆ‚g)g').
 * Thus $(MATH g' = q'/$(SUP âˆ‚Q)/$(SUB âˆ‚g)).
 *
 * Params:
 *   a = the shape parameter, must be positive
 *   q = $(MATH Q(a,x)), must be in the interval $(MATH [0,1])
 *
 * Returns:
 *   the inverse of the regularized upper incomplete gamma function evaluated at q expressed as _a
 *   `GDN`
 */
pure nothrow @nogc @safe GDN!Deg gammaIncompleteComplInverse(ulong Deg)(in real a, in GDN!Deg q)
in {
    if (!any!(std.math.isNaN)(only(a, q.val))) {
        assert(signbit(a) == 0, "the shape parameter must be positive");
        assert(q >= 0.0L && q <= 1.0L, "the argument must in [0,1]");
    }
}
out(x; isNaN!Deg(x) || x >= 0.0L, "result should be in [0,1]")
do {
    if (any!(std.math.isNaN)(only(a, q.val))) return GDN!Deg.nanCombine(q, asGDN!Deg(a));

    static if (Deg == 1)
        alias Q_inv = std.mathspecial.gammaIncompleteComplInverse;
    else
        alias Q_inv = gammaIncompleteComplInverse;

    const g_red = Q_inv(a, q.reduce());
    return GDN!Deg(asReal(g_red), -q.d/gammaIncompleteDeriv(a, asGDN!Deg(g_red)));
}
///
unittest {
    import std.math: isClose;

    const x = GDN!1(2);
    const res = gammaIncompleteComplInverse(1, gammaIncompleteCompl(1, x));
    assert(isClose(res, x) && isClose(res.d, x.d));
}
unittest {
    import std.format: format;
    import std.math: SQRT2;

    const a = gammaIncompleteComplInverse(NaN(0x1UL), GDN!1(0, NaN(0x3UL)));
    assert(isNaN(a) && getNaNPayload(a) == 0x1UL && getNaNPayload(a.d) == 0x3UL);

// NB: broken in std.mathspecial.gammaIncompleteComplInverse. fixed in master
//     const b = gammaIncompleteComplInverse(2.0L, GDN!1(1.0L));
//     // Q(2,0) = ğ›¤(2,0)/ğ›¤(2) = 1
//     // g = Qâ»Â¹(2,1) = Qâ»Â¹(2, Q(2,0)) = 0
//     // g' = -1/Qâ‚“(2,0) = 1/0 = -âˆ
//     assert(b == 0.0L, format("Qâ»Â¹(2,1) = %s", b.val));
//     assert(b.d is -real.infinity);

    assert(gammaIncompleteComplInverse(1.0L, GDN!1(1.0L/E, 2.0L)) is GDN!1(1.0L, -2.0L*E));
    // Q(1,1) = exp(-1)/ğ›¤(1) = 1/e
    // g = Qâ»Â¹(1,1/e) = Qâ»Â¹(1, Q(1,1)) = 1
    // g' = -2/Qâ‚“(1,1) = -2ğ›¤(1)/(1^(1-1)exp(-1)) = -2/exp(-1) = -2e

    const c_act = gammaIncompleteComplInverse(.5L, GDN!2(erfc(SQRT2), 1.0L, 1.0L));
    // Q(.5,2) = âˆšÏ€â‹…erfc(âˆš2)/ğ›¤(.5) = âˆšÏ€â‹…erfc(âˆš2)/âˆšÏ€ = erfc(âˆš2)
    // g = Qâ»Â¹(.5, erfc(âˆš2)) = Qâ»Â¹(.5, Q(.5,2)) = 2
    // g' = -1/Qâ‚“(.5,2) = -1â‹…ğ›¤(.5)/(2^(.5-1)exp(-2)) = -âˆšÏ€/(2^-.5â‹…e^-2) = -âˆš(2Ï€)e^2
    // <g',g"> = -<1,1>/Qâ‚“(.5, <2,g'>) = -<1,1>/(<2,g'>^(.5-1)exp(-<2,g'>)/ğ›¤(.5))
    //    = -ğ›¤(.5)<1,1><2,g'>^.5â‹…exp(<2,g'>) = -âˆšÏ€<1,1><âˆš2, g'/(2âˆš2)><e^2, g'e^2>
    //    = -âˆšÏ€e^2<âˆš2, âˆš2 + g'/(2âˆš2)><1,g'> = -âˆš(2Ï€)e^2<1, 1+g'/4><1,g'> = g'<1, 1+g'/4 + g'>
    //    = <g', g' + 5(g')^2/4> = <g', -âˆš(2Ï€)e^2 + (5/4)(-âˆš(2Ï€)e^2)^2>
    //    =  <g', -âˆš(2Ï€)e^2 + (5Ï€/2)e^4>
    const c_exp = GDN!2(2.0L, -sqrt(2.0L*PI)*E^^2, 2.5L*PI*E^^4 - sqrt(2.0L*PI)*E^^2);
    assert(
        isClose(c_act, c_exp) && isClose(c_act.d, c_exp.d) && c_act.d!2 == c_exp.d!2,
        format("Qâ»Â¹(.5, <erfc(âˆš2),1,1>) = %s â‰  %s", c_act, c_exp));

    const d = gammaIncompleteComplInverse(2.0L, GDN!2(2.0L/E, -2.0L, 1.0L));
    // Q(2,1) = âŒŠeâ‹…(2-1)!âŒ‹/e/ğ›¤(2) = âŒŠeâ‹…1!âŒ‹/e = âŒŠeâŒ‹/e = 2/e
    // g = Qâ»Â¹(2, 2/e) = Qâ»Â¹(2, Q(2,1)) = 1
    // g' = -(-2)/Qâ‚“(2,1) = 2/(1^(2-1)exp(-1)/ğ›¤(2)) = 2e/1^1 = 2e
    // <g',g"> = -<-2,1>/Qâ‚“(2,<1,g'>) = <2,-1>/(<1,g'>exp(-<1,g'>)) = <2,-1>exp(<1,g'>)/<1,g'>
    //    = <2,-1><e, g'e>/<1,g'> = <2e,-e+2eg'>/<1,g'> = e<2,2g'-1>/<1,g'>
    //    = e<2,(2g' - 1 - 2g')/1^2> = <2e, e(-1)>
    //    = <g',-e>
    assert(
        isClose(d, 1.0L) && d.d == 2.0L*E && isClose(d.d!2, -E),
        format("Qâ»Â¹(2, <2/e,-2,1>) = %s", d));

    const e_act = gammaIncompleteComplInverse(2.0L, GDN!2(3.0L/E^^2, 1.0L, -1.0L));
    // Q(2,2) = 1!exp(-2)[1/1 + 2/1]/1 = 3/e^2
    // g = Qâ»Â¹(2, 3/e^2) =  Qâ»Â¹(2, Q(2,2)) = 2
    // g' = -1/Qâ‚“(2,2) = -1/(2^(2-1)exp(-2)/ğ›¤(2)( = -1(e^2â‹…1)/2^1 = -e^2/2
    // <g',g"> = -<1,-1>/Qâ‚“(2,<2,g'>) = <-1,1>exp(<2,g'>)ğ›¤(2)/<2,g'>^(2-1)
    //    = <-1,1><e^2,g'e^2>/<2,g'> = e^2<-1,1><1,g'>/<2,g'> = e^2<-1,1-g'>/<2,g'>
    //    = e^2<-1/2, ((1-g')2 - -g')/2^2> = e^2<-1/2, (2 - g')/4> = e^2<-1/2, 1/2 - g'/4>
    //    = <-e^2/2, e^2(1/2 - g'/4)> = <g', e^2/2 - g'e^2/4>
    //    = <g', e^2/2 + e^4/8>
    const e_exp = GDN!2(2.0L, -E^^2/2.0L, E^^2/2.0L + E^^4/8.0L);
    assert(e_act == e_exp && isClose(e_act.d, e_exp.d));
    assert(
        isClose(e_act.d!2, e_exp.d!2),
        format("(âˆ‚Â²/âˆ‚qÂ²)Qâ»Â¹(2, <3/eÂ²,1,-1>) = %s â‰  %s", e_act.d!2, e_exp.d!2));
}


/* This function computes the derivative of the regularized incomplete beta
 * function I(x; a,b) with respect to x, where a and b are constants.
 *
 * I'(x; a,b) = (d/dx)B(x; a,b)/B(a,b)) where B(x; a,b) = âˆ«â‚€Ë£táµƒâ»Â¹(1-t)áµ‡â»Â¹dt is
 * the incomplete beta function.
 *
 * The integrand táµƒâ»Â¹(1-t)áµ‡â»Â¹ is Lebesgue integrable over 0 â‰¤ t â‰¤ 1. Therefore,
 * B' = xáµƒâ»Â¹(1-x)áµ‡â»Â¹ almost everywhere. x = 0 and 1 are the only values where
 * xáµƒâ»Â¹(1-x)áµ‡â»Â¹ doesn't exist for every a and b, but the one-sided limits do.
 * This algorithm defines
 * B'(0; a,b) = lim{xâ†’0âº} xáµƒâ»Â¹(1-x)áµ‡â»Â¹ = { âˆ, 0<a<1; 1, a=1; 0, a>1 }), and
 * B'(1; a,b) = lim{xâ†’1â»} xáµƒâ»Â¹(1-x)áµ‡â»Â¹ = { âˆ, 0<b<1; 1, b=1; 0, b>1 }).
 *
 * Thus I'(x; a,b) has the following form when 0 < a,b < âˆ.
 *
 *    - I'(x; a,b) = xáµƒâ»Â¹(1-x)áµ‡â»Â¹/B(a,b), 0 < x < 1
 *    - I'(0; a,b) = { âˆ, a<1; 1/B(1,b), a=1; 0, a>1 }
 *    - I'(1; a,b) = { âˆ, b<1; 1/B(a,1), b=1; 0, b>1 }
 *
 * Here are the degenerate cases of I'. Let H(x) = { 0, x<0; 1, xâ‰¥0 } be the
 * Heaviside step function in the following.
 *
 *    - I'(x; 0,b) = (d/dx)lim{aâ†’0âº} I(x; a,b) = (d/dx)(1 - H(-x)) = ğ›¿(x)
 *    - I'(x; âˆ,b) = (d/dx)lim{aâ†’âˆ} I(x; a,b) = (d/dx)H(x-1) = ğ›¿(x-1)
 *    - I'(x; a,0) = (d/dx)lim{bâ†’0âº} I(x; a,b) = (d/dx)H(x-1) = ğ›¿(x-1)
 *    - I'(x; a,âˆ) = (d/dx)lim{bâ†’âˆ} I(x; a,b) = (d/dx)[1 - H(-x)] = ğ›¿(x)
 *    - I'(x; 0,0) = (d/dx)lim{a,bâ†’0âº} I(x; a,b), does not exist
 *    - I'(x; 0,âˆ) = (d/dx)lim{aâ†’0âº,bâ†’âˆ) I(x; a,b) = (d/dx)[1 - H(-x)] = ğ›¿(x)
 *    - I'(x; âˆ,0) = (d/dx)lim{aâ†’âˆ,bâ†’0âº} I(x; a,b) = (d/dx)H(x-1) = ğ›¿(x-1)
 *    - I'(x; âˆ,âˆ) = (d/dx)lim{a,bâ†’âˆ) I(x; a,b), does not exist
 */
private pure nothrow @nogc @safe
GDN!Deg.DerivType!1 betaIncompleteDeriv(ulong Deg)(in real a, in real b, in GDN!Deg x)
{
    alias Deriv = typeof(return);

    if ((a == 0 && b == 0) || (a == real.infinity && b == real.infinity)) {
        return Deriv.nan;
    } else if (a == 0 || b == real.infinity) {
        return dirac(x.reduce());
    } else if (a == real.infinity || b == 0) {
        return dirac(x.reduce() - 1);
    } else {
        const x_red = x.reduce();
        const numerator = x_red^^(a - 1) * (1 - x_red)^^(b - 1);
        const denominator = std.mathspecial.beta(a, b);
        if (numerator == real.infinity && denominator == real.infinity) {
            // In this case, the denominator is not really infinite. It's just
            // larger than real.max. Instead of returning âˆ/âˆ = NaN, return the
            // numerator.
            static if (Deg == 1)
                return numerator;
            else
                return  Deriv(numerator.val, numerator.d/denominator);
        } else if (numerator == 0 && denominator == 0) {
            // In this case, the denominator is not really zero. It's just too
            // small to represent. Instead of return 0/0 = NaN, return the
            // numerator.
            static if (Deg == 1)
                return numerator;
            else
                return Deriv(numerator.val, numerator.d/denominator);
        } else {
            return numerator / denominator;
        }
    }
}

unittest
{
    import std.format: format;

    //
    // Tests of degree 1
    //

    // a = +0, b = +0

    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(0))));
    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(.5))));
    assert(isNaN(betaIncompleteDeriv(+0., +0., GDN!1(1))));

    // a = +0, b = .5

    assert(betaIncompleteDeriv(+0., .5, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., .5, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., .5, GDN!1(1)) == 0);

    // a = +0, b = 1

    assert(betaIncompleteDeriv(+0., 1, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., 1, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., 1, GDN!1(1)) == 0);

    // a = +0, b = 2

    assert(betaIncompleteDeriv(+0., 2, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., 2, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., 2, GDN!1(1)) == 0);

    // a = +0, b = âˆ

    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(+0., real.infinity, GDN!1(1)) == 0);

    // a = .5, b = +0

    assert(betaIncompleteDeriv(.5, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(.5, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(.5, +0., GDN!1(1)) is real.infinity);

    // a = .5, b = .5

    assert(betaIncompleteDeriv(.5, .5, GDN!1(0)) is real.infinity);

    assert(isClose(betaIncompleteDeriv(.5, .5, GDN!1(.5)), M_2_PI));
    // f' = .5^-.5*.5^-.5/B(.5,.5) = 2Î“(1)/[Î“(.5)Î“(.5)] = 2/(âˆšğœ‹âˆšğœ‹) = 2/ğœ‹

    assert(betaIncompleteDeriv(.5, .5, GDN!1(1)) is real.infinity);

    // a = .5, b = 1

    assert(betaIncompleteDeriv(.5, 1, GDN!1(0)) is real.infinity);

    assert(betaIncompleteDeriv(.5, 1, GDN!1(.5)) == SQRT1_2);
    // f' = .5^-.5/B(.5,1) = âˆš2*Î“(1.5)/[Î“(.5)Î“(1)] = âˆš2(âˆšğœ‹/2)/âˆšğœ‹ = âˆš2/2

    assert(betaIncompleteDeriv(.5, 1, GDN!1(1)) == .5);
    // f' = 1/B(.5,1) = Î“(1.5)/[Î“(.5)Î“(1)] = (âˆšğœ‹/2)/âˆšğœ‹ = 1/2

    // a = .5, b = 2

    assert(betaIncompleteDeriv(.5, 2, GDN!1(0)) is real.infinity);

    assert(betaIncompleteDeriv(.5, 2, GDN!1(.5)) == 3*SQRT2/8);
    // f' = .5^-.5*.5/B(.5,2) = (âˆš2/2)Î“(5/2)/[Î“(1/2)Î“(2)] = âˆš2(3âˆšğœ‹/4)/(2âˆšğœ‹) = 3âˆš2/8

    assert(betaIncompleteDeriv(.5, 2, GDN!1(1)) == 0);

    // a = .5, b = âˆ

    assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(.5)) == 0);
        assert(betaIncompleteDeriv(.5, real.infinity, GDN!1(1)) == 0);

    // a = 1, b = +0

    assert(betaIncompleteDeriv(1, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(1, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(1, +0., GDN!1(1)) is real.infinity);

    // a = 1, b = .5

    assert(betaIncompleteDeriv(1, .5, GDN!1(0)) == 0.5L);

    const zhh = betaIncompleteDeriv(1, .5, GDN!1(.5));
    // f' = .5^-.5/B(1,.5) = âˆš2/2
    assert(zhh == SQRT1_2, format("I'(.5; 1,.5) = %s", zhh));

    assert(betaIncompleteDeriv(1, .5, GDN!1(1)) is real.infinity);

    // a = 1, b = 1

    assert(betaIncompleteDeriv(1, 1, GDN!1(0)) == 1);

    const ooh = betaIncompleteDeriv(1, 1, GDN!1(0.5L));
    assert(ooh == 1, format("I'(.5; 1,1) = %s", ooh));

    assert(betaIncompleteDeriv(1, 1, GDN!1(1)) == 1);

    // a = 1, b = 2

    assert(betaIncompleteDeriv(1, 2, GDN!1(0)) == 2);
    // f' = 1/B(1,2) = Î“(3)/[Î“(1)Î“(2)] = 2

    assert(betaIncompleteDeriv(1, 2, GDN!1(.5)) == 1);
    // f' = .5/B(1,2) = Î“(3)/[2Î“(1)Î“(2)] = 2/2 = 1

    assert(betaIncompleteDeriv(1, 2, GDN!1(1)) == 0);

    // a = 1, b = âˆ

    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(1, real.infinity, GDN!1(1)) == 0);

    // a = 2, b = +0

    assert(betaIncompleteDeriv(2, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(2, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(2, +0., GDN!1(1)) is real.infinity);

    // a = 2, b = .5

    assert(betaIncompleteDeriv(2, .5, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, .5, GDN!1(.5)) == 3*SQRT2/8);
    // f' = .5*.5^-.5/B(2,.5) = (âˆš2/2)Î“(5/2)/[Î“(1/2)Î“(2)] = 3âˆš2/8

    assert(betaIncompleteDeriv(2, .5, GDN!1(1)) is real.infinity);

    // a = 2, b = 1

    assert(betaIncompleteDeriv(2, 1, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, 1, GDN!1(0.5)) == 1);
    // f' = .5^2/B(2,1) = 1

    assert(betaIncompleteDeriv(2, 1, GDN!1(1)) == 2);

    // a = 2, b = 2

    assert(betaIncompleteDeriv(2, 2, GDN!1(0)) == 0);

    assert(betaIncompleteDeriv(2, 2, GDN!1(0.5)) == 1.5L);
    // f' = .5*.5/B(2,2) = Î“(4)/[4Î“(2)Î“(2)] = 6/4 = 3/2

    assert(betaIncompleteDeriv(2, 2, GDN!1(1)) == 0);

    // a = 2, b = âˆ

    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(0)) is real.infinity);
    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(2, real.infinity, GDN!1(1)) == 0);

    // a = âˆ, b = +0

    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, +0., GDN!1(1)) is real.infinity);

    // a = âˆ, b = .5

    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, .5, GDN!1(1)) is real.infinity);

    // a = âˆ, b = 1

    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 1, GDN!1(1)) is real.infinity);

    // a = âˆ, b = 2

    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(0)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(.5)) == 0);
    assert(betaIncompleteDeriv(real.infinity, 2, GDN!1(1)) is real.infinity);

    // a = âˆ, b = âˆ

    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(0))));
    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(.5))));
    assert(isNaN(betaIncompleteDeriv(real.infinity, real.infinity, GDN!1(1))));

    // g' â‰  1

    assert(betaIncompleteDeriv(2, 2, GDN!1(0.5, 0.5)) == 1.5L);

    //
    // Tests of degree 2
    //

    // a = 0âº

    assert(betaIncompleteDeriv(+0., 1, GDN!2(.5)) is GDN!1(0, 0));

    // a = 1, b = 0âº

    assert(betaIncompleteDeriv(1, +0., GDN!2(.5)) is GDN!1(0, 0));
    // limit I(.5;1,0+) = 0 and derivative is dirac(g-1)==0

    // a = 1, b = 1

    assert(betaIncompleteDeriv(1, 1, GDN!2(0.0L)) is GDN!1(1, 0));

    assert(betaIncompleteDeriv(2, 2, GDN!2(.5)) is GDN!1(1.5, 0));
    // f' = <.5,1>^1(1 - <.5,1>)^1/B(2,2) = <.5,1><.5,-1>Î“(4)/[Î“(2)Î“(2)] = 6<.25,-.5 + .5> =<1.5,0>

    assert(betaIncompleteDeriv(1, 1, GDN!2(1.0L)) is GDN!1(1.0L, 0));

    // a = 1, b = âˆ

    assert(betaIncompleteDeriv(1, real.infinity, GDN!2(.5)) is GDN!1(0, 0));

    // a = âˆ, b = 1

    assert(betaIncompleteDeriv(real.infinity, 1, GDN!2(.5)) is GDN!1(0, 0));
}


/**
 * The regularized incomplete beta function $(MATH I$(SUB g)(a,b)).
 *
 * For fixed $(MATH a,b > 0), let $(MATH f(x) = I$(SUB g(x))(a,b)). Then
 * $(MATH f' = g'$(SUP dI$(SUB g))/$(SUB dg)).
 *
 * Params:
 *   Deg = the degree of g
 *   a = the first shape parameter, must be positive
 *   b = the second shape parameter, must be positive
 *   g = the argument, must belong to the interval $(MATH [0,1])
 *
 * Returns:
 *   the regularized incomplete beta function evaluated at g expressed as _a `GDN`
 */
pure nothrow @nogc @safe GDN!Deg betaIncomplete(ulong Deg)(in real a, in real b, in GDN!Deg g)
in {
    assert(isNaN(a) || signbit(a) == 0, "the first shape parameter must be positive");
    assert(isNaN(b) || signbit(b) == 0, "the second shape parameter must be positive");
    assert(isNaN!Deg(g) || (g >= 0 && g <= 1), "the argument must be in [0,1]");
}
do {
    return GDN!Deg(std.mathspecial.betaIncomplete(a, b, g.val), betaIncompleteDeriv(a, b, g)*g.d);
}

///
unittest
{
    assert(betaIncomplete(1, 1, GDN!1(0.5)) is GDN!1(0.5, 1));
}

unittest
{
    import std.format: format;

    const g = GDN!1(0.5L, 0.5L);
    const a = betaIncomplete(2, 2, g);
    assert(isClose(a, 0.5L), format("I(%s; 2,2) = %s", g, a));
    assert(a.d == 0.75L);
}

// NB: In master, but not released.
// /**
//  * The regularized incomplete beta complement function $(MATH I$(SUB g)$(SUP C)(a,b)).
//  *
//  * For fixed $(MATH a,b > 0), if $(MATH f(x) = I$(SUB g(x))$(SUP C)(a,b)), then
//  * $(MATH f' = g'$(SUP dI$(SUB g)$(SUP C))/$(SUB dg)). Since
//  * $(MATH I$(SUB g)$(SUP C) = 1 - I$(SUB g)), $(MATH I$(SUB g)$(SUP C)' = -I$(SUB g)'). Thus
//  * $(MATH f' = -g'$(SUP dI$(SUB g))$(SUB dg)).
//  *
//  * Params:
//  *   Deg = the degree of g
//  *   a = the first shape parameter, must be positive
//  *   b = the second shape parameter, must be positive
//  *   g = the argument, must belong to the interval $(MATH [0,1])
//  *
//  * Returns:
//  *   the regularized incomplete beta complement function evaluated at g expressed as _a `GDN`
//  */
// pure nothrow @nogc @safe GDN!Deg betaIncompleteCompl(ulong Deg)(in real a, in real b, in GDN!Deg g)
// in {
//     assert(isNaN(a) || signbit(a) == 0, "the first shape parameter must be positive");
//     assert(isNaN(b) || signbit(b) == 0, "the second shape parameter must be positive");
//     assert(isNaN!Deg(g) || (g >= 0 && g <= 1), "the argument must be in [0,1]");
// }
// do {
//     return GDN!Deg(
//         std.mathspecial.betaIncompleteCompl(a, b, g.val), -betaIncompleteDeriv(a, b, g)*g.d);
// }
//
// ///
// unittest
// {
//     const a = 1.0L;
//     const b = 2.0L;
//     const x = GDN!1(0.1L);
//     const i = betaIncomplete(a, b, x);
//     const icc = betaIncompleteCompl(b, a, 1.0L - x);
//     assert(isClose(icc, i) && icc.d == i.d);
// }
//
// unittest
// {
//     assert(betaIncompleteCompl(1, 1, GDN!1(0.5L, 2)) is GDN!1(0.5L, -2));
// }


/**
 * The inverse of the regularized incomplete beta function.
 *
 * If $(MATH f(x) = I$(SUB g(x))(a,b)), then $(MATH g = f$(SUP -1)(f)). For fixed $(MATH a,b > 0),
 * $(MATH f' = g'$(SUP dI$(SUB g))/$(SUB dg)). Thus $(MATH g' = f'/$(SUP dI$(SUB g))/$(SUB dg)).
 *
 * Params:
 *   Deg = the degree of Ig
 *   a = the first shape parameter, must be positive
 *   b = the second shape parameter, must be positive
 *   Ig = $(MATH I$(SUB g)(a,b)), must belong to the interval $(MATH [0,1])
 *
 * Returns:
 *   the inverse of the regularized incomplete beta function evaluated at Ig expressed as _a `GDN`
 */
pure nothrow @nogc @safe
GDN!Deg betaIncompleteInverse(ulong Deg)(in real a, in real b, in GDN!Deg Ig)
in {
    assert(isNaN(a) || signbit(a) == 0, "the first shape parameter must be positive");
    assert(isNaN(b) || signbit(b) == 0, "the second shape parameter must be positive");
    assert(isNaN!Deg(Ig) || (Ig >= 0 && Ig <= 1), "the argument must be in [0,1]");
}
do {
    static if (Deg == 1)
        alias Ig_inv = std.mathspecial.betaIncompleteInverse;
    else
        alias Ig_inv = betaIncompleteInverse;

    const g_red = Ig_inv(a, b, Ig.reduce());
    return GDN!Deg(asReal(g_red), Ig.d/betaIncompleteDeriv(a, b, asGDN!Deg(g_red)));
}

///
unittest
{
    const a = 1;
    const b = 1;
    const x = GDN!1(0.5L);
    assert(betaIncompleteInverse(a, b, betaIncomplete(a, b, x)) is x);
}

unittest
{
    import std.format: format;

    const a = betaIncompleteInverse(0.5L, 1, GDN!1(0, real.infinity));
    assert(a == 0.0L && isNaN(a.d));

    const b = GDN!1(0, 0.5L);
    const c = betaIncompleteInverse(1, 2, b);
    // dI_g/dg = 1/B(1,2) = Î“(1+2)/(Î“(1)Î“(2)) = 2
    // g' = .5/2 = .25
    assert(c is GDN!1(0, 0.25L), format("Iâ»Â¹(%s; 1,2) = %s", b, c));

    const a2 = 2.0L;
    const b2 = 1.0L;
    const x2 = GDN!2(0.5, 2, -1);
    // f = 0.25
    // f' = <2,-1><.5,2>/B(2,1) = <1,-.5+4>Î“(1+2)/(Î“(1)Î“(2)) = 2<1,3.5> = <2,7>
    const y2 = GDN!2(0.25L, 2, 7);
    const d = betaIncompleteInverse(a2, b2, y2);
    assert(d is x2, format("Iâ»Â¹(%s; 2,1) = %s", y2, d));
}


// TODO: implement the following functions.
// erf
// erfc
// normalDistribution
// normalDistributionInverse
